ASMB,L,T,C
        HED     GET GODDARD DEVICE TYPE 
        NAM     IDGDT,7 C#870328:18:46    GET DEVICE TYPE NOTE DVB00# 
        ENT     IDGDT 
        EXT     .ENTR 
* 
*               RETREIVE DEVICE TYPE FROM EQT WORD 5 AND SET BIT 6
*       IF DVB00 IS REALLY PRESENT
* 
*       CALLING SEQUENCE: 
* 
*       ITYPE=IDGDT(LU) 
* 
*       LU      - THE LOGICAL UNIT WHOSE TYPE IS SOUGHT 
* 
*       ITYPE   - = EQT5 DEVICE TYPE (LEFT ADJUSTED) WITH BIT 6 SET IF
*                 DVB00 IS MASKERADING AS EITHER DVR00 OR DVR05 
* 
LU      NOP                     THE POINTER TO THE LU 
IDGDT   NOP                     THE ENTRY POINT FOR THIS ROUTINE
        JSB     .ENTR           HAVE THE BOOKKEEPING DONE 
        DEF     LU              TELL WHERE LONG INPUT LIST STARTS 
        LDA     LU,I            GET THE LU
        LDB     1653B           GET MAX LU ALLOWED
        CMB                     GET -MAX-1
        ADB     0               ADD IN REQUESTED LU 
        SSB,RSS                 SKIP IF STILL NEGATIVE
        JMP     ERR1            GO TO FIRST ERROR 
        ADA     =B177777        CORRECT NUMBERING 
        ADA     1652B           GET DRT ADDRESS 
        LDA     0,I             GET THE DRT ENTRY 
        AND     =B77            MASK ALL BUT EQT FIELD
        ADA     =B177777        CORRECT ADDRESSING
        MPY     =B17            COMPUTE EQT ADDRESS 
        ADA     1650B           GET EQT ADDRESS 
        LDB     0               COPY FOR RECOVERY IF NEEDED 
        ADA     =B4             GETR WD5 ADDRESS
        LDA     0,I             GET WD5 FROM EQT
        ALF,ALF                 GET TYPE INTO LOWER BYTES 
        AND     =B77            MASK ALL BUT TYPE FIELD OFF 
        CPA     =B0             IF DVX00 TRY FOR X=B CASE 
        JMP     TRYX            GO OUT IF TYPE=0
        CPA     =B5             IF DVX05 TRY FOR X=B CASE 
        JMP     TRYX            GO OUT IF TYPE=5
        JMP     IDGDT,I         RETURN FOR ALL EASY CASES 
TRYX    INB                     POINT AT WD2
        STA     TYSAV           SAVE A REG FOR LATER USE
        LDA     1,I             GET FIRST ENTRY TO DRIVER 
        CMA,INA                 MAKE NEGATIVE 
        INB                     STEP TO WORD 3
        ADA     1,I             ADD IN UPPER ENTRY
        ADA     =B177300        THE X=R-B DISCRIMINATOR 
        SSA                     IF POSITIVE HAVE X=B
        JMP     XNEB            GO OUT IF X=R 
        LDA     TYSAV           RESTORE A REG 
        IOR     =B100           MARK AS X=B CASE
        JMP     IDGDT,I         RETURN RESULT TO USER 
XNEB    LDA     TYSAV           RESTORE A REG FOR RETURN
        JMP     IDGDT,I         RETURN TO USER X=R
ERR1    CLA                     RETURN ZERO FOR INVALID LU
        JMP     IDGDT,I         RETURN TO USER
TYSAV   DEF     0               A REG FOR TRYX SECTION
        END 
        END$
