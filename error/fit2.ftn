FTN77,I,Y  
$CDS ON
      SUBROUTINE FIT2(X1,X2,Y1,Y2,WY1,WY2,NPTS,PAR,EPAR,IPAR,PHI,
     +  AUX,SCALE,A,B,NPAR,TOL,NTRY,F1,F2,RCHI,R1NR,R2NR,
     +  NFREE,IERR,LUSE,IGP,FEC1,FEC2,Y1R,Y2R,RCOND)
C
C     LEAST SQUARES FITTING ROUTINE...FITS TO FUNCTIONS F1 & F2
C     WHICH MUST BE DECLARED EXTERNAL IN THE CALLING ROUTINE.
C
C          THIS ROUTINE FITS DATA IN Y1 AND Y2 TO FUNCTIONS F1 AND F2
C          RESPECTIVELY, SIMULTANEOUSLY ON THE INDEPENDENT VARIABLES
C          X1 AND X2
C
C     PARAMETERS WILL BE RETURNED IN PAR, WHICH SHOULD CONTAIN FIRST
C     GUESSES TO START.  ERRORS ARE RETURNED IN EPAR.
C
C     WEH 831101
C
      DIMENSION X1(1),X2(1),Y1(1),IPAR(1),Y1R(1),Y2R(1)
      DIMENSION Y2(1),WY1(1),WY2(1),LUSE(1)
      DOUBLE PRECISION PAR(1),EPAR(1)
      DOUBLE PRECISION A(1),B(1),AUX(1),SCALE(1)
      DOUBLE PRECISION SQ,SQ1,SQ2,R,W,EPS,X1,X2,R1,R2,WYT1,WYT2
      LOGICAL KBIT
C
      EXTERNAL F1,F2
      DOUBLE PRECISION F1,F2
C
C DIMENSION X1(NPTS),X2(NPTS),Y1(NPTS),Y2(NPTS),WY1(NPTS),WY2(NPTS)
C DIMENSION PAR(NPAR),EPAR(NPAR),AUX1(NPAR),AUX2(NPAR),SCAL(NPAR)
C DIMENSION A(NPAR*(NPAR+1)/2),B(NPAR),IPAR(NPAR)
C
      DATA EPS/1D-10/
C
      RCHI=0.
      R1NR=0.
      R2NR=0.
      NFREE=0
      RCOND=0.0
      DO 10 I=1,NPAR
        EPAR(I)=0.0D0
        SCALE(I)=0.0D0
10      CONTINUE
C
      DO 100 ITRY=1,ABS(NTRY)
        CALL ININE(A,B,NPAR)
C
        DO K=1,NPTS
          IF(KBIT(LUSE,K)) THEN
            R=Y1(K)-F1(0,X1(K),X2(K),PAR,IPAR,PHI)
            W=1.0D0/(WY1(K)*WY1(K)+SIGN(FEC1*FEC1,FEC1))
            DO I=1,NPAR
              AUX(I)=F1(I,X1(K),X2(K),PAR,IPAR,PHI)
            ENDDO
            CALL INCNE(A,B,AUX,NPAR,R,W)
C
            R=Y2(K)-F2(0,X1(K),X2(K),PAR,IPAR,PHI)
            W=1.0D0/(WY2(K)*WY2(K)+SIGN(FEC2*FEC2,FEC2))
            DO I=1,NPAR
              AUX(I)=F2(I,X1(K),X2(K),PAR,IPAR,PHI)
            ENDDO
            CALL INCNE(A,B,AUX,NPAR,R,W)
          ENDIF
        ENDDO
C
        CALL SCALER(A,B,SCALE,NPAR)
        CALL DPPCO(A,NPAR,RCOND,AUX,IERR)
        IF(IERR.NE.0) GO TO 200
        CALL DPPSL(A,B,NPAR)
        CALL DPPIN(A,NPAR)
        CALL UNSCALER(A,B,SCALE,NPAR)
C
        IAGAIN=0
        NXPNT=0
        DO 60 I=1,NPAR
          NXPNT=NXPNT+I
          DIV=DSQRT(A(NXPNT))
          IF(DIV.LT.DSQRT(EPS)) DIV=DMAX1(B(I)*10.0D0/TOL,10.0D0/TOL)
          IF(DABS(B(I)/DIV).GT.TOL) IAGAIN=1
          PAR(I)=PAR(I)+B(I)
60      CONTINUE
C
        IF(IAGAIN.EQ.0.OR.NTRY.LT.0) GO TO 101
100   CONTINUE
C
      IERR=-2
      GO TO 102
C
101   CONTINUE
      IERR=ITRY
C
102   CONTINUE
      NXPNT=0
      DO 110 I=1,NPAR
        NXPNT=NXPNT+I
        EPAR(I)=DSQRT(A(NXPNT))
        DIV=1.0D0
        IF(SCALE(I).GT.1D-16) DIV=1.0D0/SCALE(I)
        SCALE(I)=EPAR(I)*DIV
110   CONTINUE
C
      SQ=0.0D0
      SQ1=0.0D0
      SQ2=0.0D0
      DO 120 I=1,NPTS
        R1=Y1(I)-F1(0,X1(I),X2(I),PAR,IPAR,PHI)
        R2=Y2(I)-F2(0,X1(I),X2(I),PAR,IPAR,PHI)
        Y1R(I)=R1
        Y2R(I)=R2
        IF(.NOT.KBIT(LUSE,I)) GO TO 120
        WYT1=1.0/(WY1(I)*WY1(I)+SIGN(FEC1*FEC1,FEC1))
        WYT2=1.0/(WY2(I)*WY2(I)+SIGN(FEC2*FEC2,FEC2))
        SQ=SQ+R1*R1*WYT1+R2*R2*WYT2
        SQ1=SQ1+R1*R1*WYT1
        SQ2=SQ2+R2*R2*WYT2
120   CONTINUE
C
      IAPAR=0
      DO 115 I=1,NPAR
         IF(IPAR(I).NE.0.AND.SCALE(I).GT.0.01D0) IAPAR=IAPAR+1
115   CONTINUE
C
      NFREE=2*IGP-IAPAR
      IF(NFREE.LE.0) RETURN
C
      RCHI=DSQRT(SQ/NFREE)
      R1NR=DSQRT(SQ1/IGP)
      R2NR=DSQRT(SQ2/IGP)
      NXPNT=0
      DO 140 I=1,NPAR
        NXPNT=NXPNT+I
        EPAR(I)=RCHI*EPAR(I)
140   CONTINUE
      RETURN
C
200   CONTINUE
      IERR=-1
      END
