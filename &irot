ASMB
      HED <BIT ROTATER  ARW 771229> 
      NAM IROT,7 Left/right bit rotater  ARW 771229 
      ENT IROT
      EXT .ENTR 
* 
* FUNCTION TO ROTATE BITS WITHIN A 16-BIT WORD
* 
* K=IROT(M,N) WILL ROTATE THE CONTENTS OF M BY N BITS (-LEFT,+RIGHT)
*             AND SET THE RESULT INTO K  (-16<=N<=16) 
* 
M     BSS 1 
N     BSS 1 
IROT  NOP 
      JSB .ENTR 
      DEF M 
      LDA M,I 
      LDB N,I 
      SZB,RSS       N=0?
      JMP IROT,I     YES. RETURN
      LDA RRL       SET (A)="RRL 16"
      SSB,RSS       N<0?
      IOR =B1000     NO. CONSTRUCT "RRR 16" INSTRUCTION.
      STA R1         YES. 
      LDA N,I 
      SSA           N<0?
      CMA,INA        YES. CHANGE SIGN.
      AND =B17
      IOR R1
      STA R1
      LDA M,I 
      CLB 
R1    NOP 
      IOR B 
      JMP IROT,I
RRL   RRL 16
A     EQU 0 
B     EQU 1 
      END 
