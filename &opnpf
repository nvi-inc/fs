FTN4,X
      SUBROUTINE OPNPF(LPRC,IDCB,IBUF,IBLEN,ICRPRC,LPROC,MAXPR,NPROC
     .,IERR),OPEN PROC FILE AND MAKE DIRECTORY C#870115:04:19#
C 
C     OPNPF opens the named procedure file and reads through
C     it, making a directory of the entries.
C 
C     DATE   WHO CHANGES
C     810906 NRV CREATED
C 
C  INPUT: 
C 
C     LPRC - the last 2 char of the file name 
C     IDCB - DCB for opening the file 
C     IBUF - a buffer to use for reading
C     IBLEN - the length of IBUF in words 
C     ICRPRC - the cartridge for the proc files 
C     MAXPR - maximum number of entries in LPROC
      DIMENSION IDCB(1),IBUF(1) 
C 
C  OUTPUT:
C 
C     LPROC - the procedure directory information 
C     NPROC - number of procedures in this file 
      DIMENSION LPROC(5,1)
C 
C  LOCAL: 
C 
C     IERR - error return from FMP routines 
C     ICH - character counter 
C     LNAMEF - full file name 
      DIMENSION LNAMEF(3) 
D     DATA LU/24/ 
C 
C 
C     1. First try to open the file.
C     The file is opened non-exclusively and in update mode.
C 
      LNAMEF(1) = 2H[P
      LNAMEF(2) = 2HRC
      LNAMEF(3) = LPRC
      CALL OPEN(IDCB,IERR,LNAMEF,3,0,ICRPRC)
D     WRITE(LU,9911) LPRC,LNAMEF,ICRPRC,MAXPR 
      IF (IERR.LT.0) GOTO 990 
C 
D9911 FORMAT("IN OPNPF: LPRC,LNAMEF,ICRPRC,MAXPR="A2,1X,3A2,2I4)
C 
C     2. The file is opened.  Read through all lines. 
C     If we encounter DEFINE, then execute next section.
C 
200   NPROC = 0 
210   CALL READF(IDCB,IERR,IBUF,IBLEN,ILEN) 
      IF (ILEN.LT.0) GOTO 900 
      IF (IERR.LT.0) GOTO 990 
      IF (ICHCM(IBUF,1,6HDEFINE,1,6).EQ.0.AND.ILEN.EQ.17) GOTO 300
      GOTO 210
C 
C 
C     3. Pick up the name and hash it.  Check for duplicates. 
C     Fill up LPROC with the information on this file location. 
C 
300   IF (NPROC+1.LE.MAXPR) GOTO 310
      CALL LOGIT(0,0,0,1,-126,2HBO,MAXPR) 
      GOTO 990
310   IC = IFLCH(IBUF,20) 
C                     Find the last character of the procedure name 
      ICODE = IHASH(IBUF(5),1,IC-8) 
      NPROC = NPROC + 1 
      IF (NPROC.EQ.1) GOTO 322
      IDUP = 0
      DO 321 II=1,NPROC-1 
        IF (ICODE.NE.LPROC(1,II)) GOTO 321
        CALL LOGIT(0,0,0,1,-113,2HBO,NPROC*100+II)
D       IOUT1=IH22A(ICHAR(ICODE,1)) 
D       IOUT2=IH22A(ICODE)
D       WRITE(LU,9903)ICODE,IOUT1,IOUT2 
D9903   FORMAT("ICODE,IOUT = "I5,2X,2A2)
        IDUP = 1
321     CONTINUE
      IF (IDUP.EQ.0) GOTO 322 
      NPROC = NPROC - 1 
      GOTO 210
C                     If there was a duplicate, don't count it. 
322   LPROC(1,NPROC) = ICODE
      CALL POSNT(IDCB,IERR,-1)
      CALL LOCF(IDCB,IERR,IREC,IRB,IOFF)
      LPROC(2,NPROC) = IREC 
      LPROC(3,NPROC) = IRB
      LPROC(4,NPROC) = IOFF 
      LPROC(5,NPROC) = LPRC 
      CALL POSNT(IDCB,IERR,+1)
      GOTO 210
C 
C 
C     9. Normal ending because EOF is reached.
C 
900   IERR = 0
C 
C 
990   CALL RWNDF(IDCB)
D     WRITE(LU,9901)LPRC,((LPROC(I,J),I=1,5),J=1,NPROC) 
D9901 FORMAT("LPROC="A2/(5I5/)) 
      RETURN
      END 
