ASMB,R,B,L
        HED     CHARACTER MOVE ROUTINE
* 
*@ICHMV 
* 
*       INTEGER FUNCTION ICHMV(IOUT,IOI,IN,INI,N) 
* 
*     ICHMV 
* 
* 1.  NAME PROGRAM SPECIFICATION
* 
* 1.1.   ICHMV
* 
*               THIS ROUTINE MOVES N CHARACTERS FROM IN TO IOUT STARTING
*       AT CHARACTER INI IN IN AND MOVING INTO CHARACTER IOI IN IOUT
*       INITIALLY.
* 
* 1.2.   RESTRICTIONS - limits on use of routine
* 
* 1.3.   REFERENCES - document cited
* 
* 2.  NAME INTERFACE
* 
* 2.1.   CALLING SEQUENCE: IEND=ICHMV(IOUT,IOI,IN,INI,N)
* 
*     INPUT VARIABLES:
* 
*       IOI     - INITIAL CHARACTER POSITION TO FILL IN IOUT
* 
*       INI     - INITIAL CHARACTER TO TAKE IN IN 
* 
*       N       - NUMBER OF CHARACTERS TO MOVE
* 
*       DIMENSION IN(2) 
* 
*       IN      - THE INPUT CHARACTERS TO BE MOVED TO IOUT
* 
*     OUTPUT VARIABLES: 
* 
*       ICHMV   - THE LAST CHARACTER +1 FILLED IN IOUT
* 
*       DIMENSION IOUT(2) 
* 
*       IOUT    - THE STRING INTO WHICH THE CHARACTERS ARE MOVED
* 
* 2.5.   SUBROUTINE INTERFACE:
* 
*       THIS IS A UTILITY TO BE CALLED BY ANYONE
* 
*     CALLED SUBROUTINES: NONE
* 
* 3.  LOCAL VARIABLES 
* 
* 
* 6.  PROGRAMMER: LEE N. FOSTER 
*     LAST MODIFIED:
* 
        NAM     ICHMV,7 C#890516:19:13  CHARACTER MOVER V=1.0 # 
        ENT     ICHMV 
        EXT     .ENTR 
IOUT    BSS     1 
IOI     BSS     1 
IN      BSS     1 
INI     BSS     1 
N       BSS     1 
ICHMV   NOP             MAIN ENTRY POINT
        JSB     .ENTR   MOVE PARAMETERS INTO ROUTINE
        DEF     IOUT
        LDA     IN      GET START WORD ADDRESS
        RAL             MAKE A BYTE ADDRESS 
        ADA     DMONE   ADJUST FOR ZERO BYTE PROBLEM
        ADA     INI,I   GET INPUT BYTE ADDRESS
        LDB     IOUT    GET OUTPUT WORD ADDRESS 
        RBL             MAKE A WORD ADDRESS 
        ADB     DMONE   HANDLE ZERO BYTE ADDRESSING PROBLEM 
        ADB     IOI,I   GET OUTPUT BYTE ADDRESS 
        MBT     N,I     MOVE N BYTES FROM IN TO IOUT
        LDA     IOI,I   COMPUTE NEXT BYTE TO BE FILLED IN IOUT
        ADA     N,I     LEAVE NEXT BYTE POINTER IN A
        JMP     ICHMV,I RETURN TO THE CALLER
DMONE   DEC     -1
        END 
