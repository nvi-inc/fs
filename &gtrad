FTN4,X
C@GTRAD 
C 
      SUBROUTINE GTRAD(IAS,IC1,IC2,IMODE,RAD, 
     .     IERR),GET RA,DEC FORMATS C#870115:04:30# 
C 
C 1.1.   GTRAD decodes the SNAP ra, dec formats 
C 
C     INPUT VARIABLES:
C 
      DIMENSION IAS(1)
C               - string array
C        IC1,IC2 - first, last characters of time format in IAS 
C        IMODE  - mode allowed for input characters 
C                 1 = RA
C                 2 = DEC with limits +/-90 
C                 3 = RA offsets (can be negative)
C                 4 = degrees, limits +/-360
C 
C     OUTPUT VARIABLES: 
C 
C        RAD - value of angle, in radians 
C        IERR   - error return, 0=all OK
C 
C     CALLING SUBROUTINES: SORCE
C 
C     CALLED SUBROUTINES: ICHMV,IAS2B,ISCNC 
C 
C 3.  LOCAL VARIABLES 
C 
      DOUBLE PRECISION DAS2B
C        LEC    - last character of IAS 
C        ICD    - character position of decimal point 
C        NFPC   - number of characters beyond decimal point 
C        ICH    - scan character
C        ICHX   - place-keeping scan character
C        NSCAN  - number of char scanned so far 
C        IHR    - hours 
C        IMIN   - minutes 
C        ISEC   - seconds 
C        IFSEC  - fractional seconds
      DIMENSION ISUF(2) 
C               - suffixes to be scanned for
      DIMENSION FLD(3)
C               - H,M,S values
C 
C 4.  CONSTANTS USED
C 
C 5.  INITIALIZED VARIABLES 
C 
      DATA ISUF   /2HHM,2HS / 
      DATA PI/3.141592/ 
C 
C 6.  PROGRAMMER: NRV 
C     LAST MODIFIED: CREATED 800216 
C# LAST COMPC'ED  870115:04:30 #
C 
C 
C     1. Initialize.  Check last character: if numeric, we have 
C     the fully numeric format, otherwise suffixes. 
C 
      IERR = 0
      RAD = 0.0 
      IFC = IC1 
      IEC = IC2 
      CALL ICHMV(ISUF,1,2HH ,1,1) 
      IF (IMODE.EQ.2.OR.IMODE.EQ.4) CALL ICHMV(ISUF,1,2HD ,1,1) 
C                   If declination, change first suffix to "D"
      DO 100 I=1,3
        FLD(I) = 0.0
100     CONTINUE
D     WRITE(26,9902) ISUF 
D9902 FORMAT("SUFFIXES THIS PASS="3A2)
C 
      LFC = ICHAR(IAS,IFC)
      ISIGN = +1
      IF (LFC.NE.53B.AND.LFC.NE.55B) GOTO 190 
C                   If there is no sign, no work needs to be done 
      IF (IMODE.NE.1) GOTO 180
      IERR = -3 
C                   The RA cannot be negative unless it is an offset! 
      GOTO 999
180   IFC = IFC + 1 
C                   Skip over the sign
      IF (LFC.EQ.55B) ISIGN = -1
C                   Remember the sign for signing the value at the end
190   LEC = ICHAR(IAS,IEC)
      IF ((LEC.LT.60B.OR.LEC.GT.71B).AND.LEC.NE.56B) GOTO 500 
C                   If last char is non-numeric AND not a "." 
C                   we must have some suffixes
C 
C 
C     2. Fully numeric format.  Locate the
C     decimal point, if any, and pull off seconds, minutes, then
C     hours from the right. 
C     General format is:
C                   hhmmss.sss
C                   \----/\--/
C                    time  fraction 
C # chars each:        6   NFPC 
C 
C 
      ICD = ISCNC(IAS,IFC,IEC,56B)
C                    Scan for decimal point 
      IF (ICD.EQ.0) ICD = IEC + 1 
      NFPC = IEC - ICD + 1
C                   Number of digits past decimal point, INCL . 
      IF (ICD-6.GE.IFC) GOTO 300
      IERR = -1 
      GOTO 999
C                   Must use suffixes if not completely specified 
C 
C 
C     3.  Decode H, M, S now.  Start from decimal point and 
C     proceed to the left.
C 
300   FLD(3)= DAS2B(IAS,ICD-2,NFPC+2,IERR)
      IMIN = IAS2B(IAS,ICD-4,2) 
      IHR  = IAS2B(IAS,ICD-6,2) 
      IF (IERR.NE.0.OR.IMIN.EQ.-32768.OR.IHR.EQ.-32768) GOTO 390
      FLD(1) = IHR
      FLD(2) = IMIN 
      GOTO 600
C 
390   IERR = -2 
      GOTO 999
C 
C 
C     5. Alternate format time, with suffixes.
C 
500   NSCAN = 0 
      ICHX = IFC-1
520   DO 530 I=1,3
D     IA = ICHAR(ISUF,I)
D     WRITE(26,9900) I,ICHX,ICH,IEC,NSCAN,IA,FLD(I) 
D9900 FORMAT("START OF LOOP, I="I1",ICHX,ICH,IEC,NSCAN="4I5,
D    ."SUF,FLD="A2,F10.3) 
        ICH = ISCNC(IAS,ICHX+1,IEC,ICHAR(ISUF,I)) 
D     WRITE(26,9901) ICH
D9901 FORMAT("ICH AFTER SCAN="I5) 
        IF (ICH.EQ.0) GOTO 530
        FLD(I) = DAS2B(IAS,ICHX+1,ICH-ICHX-1,IERR)
D     WRITE(26,9903) FLD(I),IERR
D9903 FORMAT("FLD,IERR="F10.3,I5) 
        IF (IERR.NE.0) GOTO 590 
        NSCAN = NSCAN + ICH - ICHX
        ICHX = ICH
        IF (ICHX.GE.IEC) GOTO 600 
530     CONTINUE
C 
C 
590   IERR = -8 
      GOTO 999
C 
C 
C     6. Check range for H,M,S. 
C     If all is OK, compute value in radians. 
C 
600   IF (IMODE.EQ.1.AND.(FLD(1).LT.0.0.OR.FLD(1).GT.24.0)) GOTO 650
      IF (IMODE.EQ.2.AND.(FLD(1).LT.0.0.OR.FLD(1).GT.90.0)) GOTO 650
      IF (IMODE.EQ.4.AND.(FLD(1).LT.0.0.OR.FLD(1).GT.360.0)) GOTO 650 
      IF (FLD(2).LT.0.0.OR.FLD(2).GT.60.0) GOTO 650 
      IF (FLD(3).LT.0.0.OR.FLD(3).GT.60.0) GOTO 650 
C 
      FAC = 1.0 
      IF (IMODE.EQ.2.OR.IMODE.EQ.4) FAC = 15.0
      RAD = ISIGN*(FLD(1)*3600.0+FLD(2)*60.0+FLD(3))* 
     .      PI/(3600.0*12.0*FAC)
C 
      GOTO 999
C 
650   IERR = -7 
C 
999   RETURN
      END 
