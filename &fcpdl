FTN4,X
      SUBROUTINE FCPDL(ICRDIR,LCROUT,NCROUT,LNAMEF,ICRFIL,
     .IERR),MAKE FCOPY FILE DIRECTORY C#870115:05:28# 
C 
C     FCPDL writes a file containing a directory of a cartridge.
C     The file is appropriate for use by FCOPY. 
C     Output file name is LNAMEF on ICRFIL. 
C     Form of each line in the output file: 
C               <file>:<sc>:<cart>,::<out>  
C     where <file> is the name of disk file on ICRDIR 
C           <sc> is the security code of the file 
C           <cart> is the cartridge (NOT LU) number of ICRDIR 
C           <out> is the target disk, may be a string to be later 
C                 edited, from LCROUT and NCROUT
C 
C  INPUT: 
C 
C     ICRDIR - cartridge (or negative LU) for which a 
C              directory is wanted
      DIMENSION LCROUT(1) 
C      - string with the output unit
C     NCROUT - number of characters in LCROUT 
      DIMENSION LNAMEF(3) 
C            - file name for output in FCOPY format 
C     ICRFIL - cartridge (or negative LU) for output file 
C 
C  OUTPUT:
C 
C     IERR - error return, from CREAT 
C            error -999 means error in IDTKX
C 
C  SUBROUTINES CALLED: IDTKX - gets first directory track and cartref 
C 
C 
C  LOCAL: 
C 
C     ICART - cartridge corresponding to ICRDIR, which may be an LU 
C     IDTRK1, IDTRK2 - first, last directory tracks 
C     NSECT - number of sectors per track 
      DIMENSION IBUF2(20),IDCB(144) 
C      - buffer and DCB for output file 
      DIMENSION IBUF(128) 
C      - buffer for IDTKX and EXEC(1) 
C     LUCR - LU returned from IDTKX 
      DIMENSION LABEL(3)
C      - cartridge label
C 
C  INITIALIZED: 
C 
D     DATA LUUSR/16/
C 
C 
C     1.0 Initialize. 
C 
      CALL IDTKX(IBUF,ICRDIR,IDTRK1,LUCR,ICART,NSECT) 
      IERR = -999 
      IF (IDTRK1.EQ.0) GOTO 900 
      CALL EXEC(1,100B+LUCR,IBUF,128,IDTRK1,0)
      IDTRK2 = IBUF(8)
      CALL ICHMV(LABEL,1,IBUF,1,6)
      LABEL(1) = IAND(LABEL(1),77777B)
D     WRITE(LUUSR,9902) ICR,IDTRK1,IDTRK2,LUCR,LABEL,NSECT
D9902 FORMAT("ICR,IDTRK1&2,LUCR,LABEL,NSECT="/4I5,1X,3A2,I5)
C 
110   CALL CREAT(IDCB,IERR,LNAMEF,10,3,0,ICRFIL)
      IF (IERR.GE.0) GOTO 200 
      IF (IERR.NE.-2) GOTO 900
      CALL PURGE(IDCB,IERR,LNAMEF,0,ICRFIL) 
      GOTO 110
C 
C 
C     2. Loop through directory tracks and get each file name.
C 
200   DO 290 IDTRK=IDTRK1,IDTRK2,-1 
        DO 280 I=1,NSECT/2
          ISECT = MOD(14*(I-1),NSECT) 
          CALL EXEC(1,100B+LUCR,IBUF,128,IDTRK,ISECT) 
          JENT = 1
          IF (IDTRK.EQ.IDTRK1.AND.ISECT.EQ.0) JENT = 2
C                     Skip first entry on first dir trk 
          DO 270 J=JENT,8 
            IF (IBUF(((J-1)*16)+1).LE.0) GOTO 270 
C                     Skip empty or purged entries
            IF (ICHCM(IBUF(((J-1)*16)+1),1,6HFLOPLK,1,6).EQ.0) GOTO 270 
            IF (ICHCM(IBUF(((J-1)*16)+1),1,LNAMEF,1,6).EQ.0) GOTO 270 
C                   Skip the FLOPLK file and our own name 
            ISCODE = IBUF(((J-1)*16)+9) 
            CALL ICHMV(IXTNT,2,IBUF(((J-1)*16)+6),1,1)
C                      The extent is in upper byte
D9901       FORMAT(1X,3A2":"I5":"I5", EXT="I5)
            IF (IXTNT.NE.0) GOTO 270
C                      Skip the extent entries
            NCH = ICHMV(IBUF2,1,IBUF(((J-1)*16)+1),1,6) 
D           WRITE(LUUSR,9901) (IBUF2(K),K=1,3),ISCODE,LUCR,IXTNT
            NCH = ICHMV(IBUF2,NCH,2H: ,1,1) 
            NCH = NCH + IB2AS(ISCODE,IBUF2,NCH,100000B+6) 
            NCH = ICHMV(IBUF2,NCH,2H: ,1,1) 
            NCH = NCH + IB2AS(ICART,IBUF2,NCH,100000B+6)
            NCH = ICHMV(IBUF2,NCH,3H,::,1,3)
            NCH = ICHMV(IBUF2,NCH,LCROUT,1,NCROUT)
            CALL IFILL(IBUF2,NCH,1,40B) 
            CALL WRITF(IDCB,IERR,IBUF2,NCH/2) 
270         CONTINUE
280     CONTINUE
290   CONTINUE
900   CONTINUE
      CALL CLOSE(IDCB)
      END 
