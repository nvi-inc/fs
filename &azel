FTN4,X
C@AZEL
C 
      SUBROUTINE AZEL(IP),AZEL POINTING COMMAND C#870115:04:44# 
C 
C 1.1  AZEL SETS THE LOCAL AZIMUTH AND ELEVATION FOR ANTENNA POINTING 
C 
C   INPUT VARIABLES:
C 
      DIMENSION IP(1) 
C             -PARAMETERS FROM QUIKR
C      IP(1)  -CLASS NUMBER OF INPUT BUFFER 
C      IP(2-5)-NOT USED 
C 
C   OUTPUT VARIABLES: 
C 
C      IP(1) - CLASS NUMBER 
C      IP(2) - NUMBER OF RECORDS
C      IP(3) - ERROR NUMBER 
C      IP(4) - WHO WE ARE 
C 
C 2.2  COMMON BLOCKS USED 
C 
      INCLUDE #FSCOM
C 
C   CALLING SUBROUTINE: QRANT 
C   CALLED SUBROUTINES: CHARACTER STRING UTILITIES
C 
C 3.  LOCAL VARIABLES 
C 
C     TAZC      - Temporary commanded azimuth holder
C     TELC      - Temporary commanded elevation holder
      DIMENSION IBUF(20)
C               - Class buffer
      DIMENSION IREG(2) 
C               - Registers from EXEC calls 
      DIMENSION MBUF(6) 
      EQUIVALENCE (REG,IREG(1)) 
C 4.  CONSTANTS USED
C 
C 5.  INITIALIZED VARIABLES 
C 
      DATA ILEN/40/ 
C               - Buffer length 
C 
C 6.  PROGRAMMER (ARIES)RBM MODIFIED BY NRV 
C     LAST MODIFIED 810510
C 
C     PROGRAM STRUCTURE 
C 
C     1. If class buffer contains command with "=", then process
C     the commanded azimuth and elevation. If only command name 
C     is present, then read the current antenna az and el from common.
C 
      ICLCM=IP(1) 
      IF(ICLCM.NE.0)GO TO 110 
      IERR=-1 
      GO TO 990 
  110 REG=EXEC(21,ICLCM,IBUF,-ILEN) 
      NCHAR=IREG(2) 
      IEQ=ISCNC(IBUF,1,NCHAR,75B) 
C                  Scan for "=" 
      IF(IEQ.EQ.0)GO TO 500 
C 
C     2. Step through the buffer getting each parameter and decoding it.
C     Command from the user has these parameters: 
C                 AZEL=<AZIMUTH>,<ELEVATION> in Decimal Degrees 
C 
      ICH=IEQ+1 
      CALL GTPRM(IBUF,ICH,NCHAR,2,PARM) 
C                   Get azimuth 
      IF(ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B)GO TO 200
      IF(ICHAR(PARM,1).EQ.52B)AZC=AZIMC*180./PI 
C                   Check for "*" indicating no change in azimuth 
      IF(ICHAR(PARM,1).NE.54B)GO TO 250 
C                   No default allowed for azimuth
      IERR=-101 
      GO TO 990 
  200 AZC=PARM
      IF(AZC.GE.0.0.AND.AZC.LE.360.)GO TO 250 
C                   Commanded azimuth within limits?
      IERR=-201 
      GO TO 990 
C 
C     Now get elevation 
C 
  250 CALL GTPRM(IBUF,ICH,NCHAR,2,PARM) 
      IF(ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B)GO TO 260
      IF(ICHAR(PARM,1).EQ.52B)ELC=ELEVC*180./PI 
C                   Check for "*" indicating no change in elevation 
      IF(ICHAR(PARM,1).NE.54B)GO TO 300 
C                   No default allowed for elevaton 
      IERR=-102 
      GO TO 990 
  260 ELC=PARM
      IF(ABS(ELC).LE.90)GO TO 300 
C                   Commanded elevation within limits?
      IERR=-202 
      GO TO 990 
C 
C     3. Put coordinates into field common. 
C 
  300 AZIMC=AZC*PI/180. 
      ELEVC=ELC*PI/180. 
      CALL ICHMV(LSORNA,1,8HAZEL    ,1,8) 
C                   Put the source name code into common
C 
C     4. Now schedule antenna pointing
C 
C     CALL ICHMV(MBUF,1,12HBR,TRK0     ,1,12) 
C     IANTCM=IANTCM.AND.7 
C     I=MESSS(MBUF,12)
      CALL EXEC(23,6HANTCN ,1)
      CALL RMPAR(IP)
      RETURN
C 
C     5. This section is processed as a result of an AZEL command.
C     The field common values of the commanded and current az and el
C     will be placed in a buffer to be logged.
C 
  500 CONTINUE
      NCH=NCHAR+1 
      NCH=ICHMV(IBUF,NCH,2H/ ,1,1)
C                   Put / to indicate a response
      NCH=NCH+IR2AS(AZIMC*180./PI,IBUF,NCH,6,2) 
C                   Commanded az is now in buffer 
      NCH=ICHMV(IBUF,NCH,2H, ,1,1)
      NCH=NCH+IR2AS(ELEVC*180./PI,IBUF,NCH,5,2) 
C                   Commanded el now in buffer
      NCH=ICHMV(IBUF,NCH,2H, ,1,1)
      NCH=NCH+IR2AS(AZIM*180./I,IBUF,NCH,6,2) 
C                   Current az in buffer
      NCH=ICHMV(IBUF,NCH,2H, ,1,1)
      NCH=NCH+IR2AS(ELEV*180./I,IBUF,NCH,5,2) 
C                   Current elevation now in buffer 
      ICLASS=0
      IERR=0
      NCH=NCH-1 
      CALL EXEC(20,0,IBUF,-NCH,0,0,ICLASS)
C                   Buffer is sent to SAM 
      IP(1)=ICLASS
      IP(2)=1 
      IP(3)=IERR
      IP(4)=2HQP
      IP(5)=0 
      RETURN
C 
C     9. ERROR EXIT 
C 
  990 IP(1)=0 
      IP(2)=0 
      IP(3)=IERR
      IP(4)=2HQP
      RETURN
      END 
