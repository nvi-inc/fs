FTN4,X
      SUBROUTINE NEWSK(IBUF,IC1,NCHAR,IDCBSK,IBLEN,IERR,  
     .ICURLN,ILSTLN),NEW SCHEDULE COMMAND C#870115:04:20# 
C 
C  NEWSK handles the starting of a new schedule for BOSS
C 
C  INPUT: 
C 
C     IBUF - input buffer with command and parameters 
C     NCHAR - number of characters in IBUF (i.e. last char) 
C     IC1 - first character of the field we are to process
C     IDCBSK - schedule file DCB, already opened OK 
C     IBLEN - available length of IBUF
      DIMENSION IBUF(1),IDCBSK(1) 
C 
C  COMMON:
C 
      INCLUDE #FSCOM::FS
C 
C 
C  LOCAL: 
C 
      LOGICAL KPAST,KTIME 
      DIMENSION IT(5),IB(20)
      DATA  IBL/20/ 
C                     For system time 
C     IREC,IRB,IOFF - for LOCF
C 
D     DATA LU/16/ 
C 
C  First check 1st line of schedule for experiment name and year
      CALL READF(IDCBSK,IERR,IB,IBL,ILEN) 
      IF(ILEN.GT.0) GOTO 50 
        CALL POSNT(IDCBSK,IERR,-1)
        IERR = -1 
        GOTO 100
50    IF(ICHAR(IB,1).NE.42B) GOTO 100 
      NCH = 2 
C  If 1st line is a comment, get experiment name here 
      CALL GTFLD(IB,NCH,IBL*2,ICF,IC2)
      IF(ICF.EQ.0) GOTO 100 
      NC = MIN0(8,IC2-ICF+1)
      CALL IFILL(LEXPER,1,8,40B)
      CALL ICHMV(LEXPER,1,IB,ICF,NC)
C  Now get the year of the schedule 
      CALL GTFLD(IB,NCH,IBL*2,ICF,IC2)
      IF(ICF.EQ.0) GOTO 100 
      NC = MIN0(4,IC2-ICF+1)
      IY = IAS2B(IB,ICF,NC) 
      IF(IY.LT.0) GOTO 100
      IF(IY.LT.100) IY = IY+1900
C  Make sure schedule year is same as current year
      CALL EXEC(11,IT,IYR)
      IF(IY.EQ.IYR) GOTO 60 
        CALL LOGIT(0,0,0,0,-137,2HBO) 
        IERR = -1 
        GOTO 900
60    IYEAR = IY
C 
100   CALL RWNDF(IDCBSK)
      ILSTLN=32767
      NLINES=0
      IC2=ISCNC(IBUF,IC1,NCHAR,54B)+1 
      IF(IC2.EQ.1) IC2=NCHAR+2
      IF(IC2.GT.NCHAR)GOTO 105
      NLINES=IAS2B(IBUF,IC2,NCHAR-IC2+1)
      IF(NLINES.GE.0) GOTO 105
        CALL LOGIT(0,0,0,0,-138,2HBO) 
        IERR=-1 
        GOTO 900
C     1. Determine the type of parameter which was input with the comand. 
C        null = start with SOURCE before now+5min.
C        time = start with SOURCE before time.
C        #nnn = start with line nnn in file.
C 
105   IREC = -1 
      IF (NCHAR.GE.IC1.AND.IC2.GT.IC1+1) GOTO 110 
      CALL EXEC(11,IT)
C                     Get current time ...
      CALL IADT(IT,5,3) 
C                     ... and add 5 minutes to it 
      GOTO 200
C                            #
110   IF (ICHAR(IBUF,IC1).EQ.43B) GOTO 120  
      CALL GTTIM(IBUF,IC1,IC2-2,0,IT1,IT2,IT3,IERR) 
      IF (IERR.GE.0) GOTO 111 
      CALL LOGIT(0,0,0,0,IERR,2HSP) 
      GOTO 900
111   IT(5) = MOD(IT1,1024) 
      IT(4) = IT2/60
      IT(3) = MOD(IT2,60) 
      IT(2) = IT3/100 
      IT(1) = 0 
      GOTO 200
C 
C     1.2 The case of #nnn - just position to the line and we're done.
C 
120   ILINE = IAS2B(IBUF,IC1+1,IC2-IC1-2) 
      IERR = 0
      IF (ILINE.GE.0) GOTO 125
      CALL LOGIT(0,0,0,1,-106,2HBO,ILINE) 
      IERR = -1 
      GOTO 900
125   IF (ILINE.LE.1) GOTO 800
      CALL POSNT(IDCBSK,IERR,ILINE-1) 
C                     Space down to the requested line
      IF (IERR.GE.0) GOTO 800 
      CALL LOGIT(0,0,0,1,-134,2HBO,IERR)
      GOTO 900
C 
C 
C     2. Search for a time in schedule file.
C     Desired time is in IT (HP format).
C 
200   CONTINUE
D     WRITE(LU,9901) IT 
D9901 FORMAT("REQUESTED TIME="5I5)
      CALL READF(IDCBSK,IERR,IBUF,IBLEN,ILEN) 
      IF (ILEN.GT.0) GOTO 201 
      IERR = -1 
      CALL LOGIT(0,0,0,0,-124,2HBO) 
      CALL CLOSE(IDCBSK)
      GOTO 900
201   IF (ICHCM(IBUF,1,7HSOURCE=,1,7).NE.0) GOTO 202
      CALL LOCF(IDCBSK,IERR,IREC,IRB,IOFF)
      KTIME=.FALSE. 
D     WRITE(LU,9902) IREC 
D9902 FORMAT("SOURCE COMMAND AT RECORD "I5) 
C                     Remember the location of the last SOURCE command
      GOTO 200
202   CONTINUE
      IF(KTIME)GO TO 200
      IF (ICHAR(IBUF,1).NE.41B) GOTO 200
C                           ! -- a wait-for command 
      IEC = IFLCH(IBUF,ILEN*2)
      CALL GTTIM(IBUF,2,IEC,0,IT1,IT2,IT3,IERR) 
      KTIME=.TRUE.
D     WRITE(LU,9903) IT1,IT2,IT3
D9903 FORMAT("TIME IN SCHEDULE="3I5)
      IF (KPAST(IT1,IT2,IT3,IT)) GOTO 200 
C                   If this time is in the past, go back and read some more 
      IF (IREC.GT.0) GOTO 220 
      CALL LOGIT(0,0,0,0,-132,2HBO) 
      IERR = -1 
      GOTO 900
220   CALL APOSN(IDCBSK,IERR,IREC,IRB,IOFF) 
C                     Re-position to the source command 
      CALL POSNT(IDCBSK,IERR,-1)
      CALL LOCF(IDCBSK,IERR,ILINE)
      IERR = 0
C 
C 
800   CALL LOCF(IDCBSK,IERR,ICURLN) 
      ICURLN=ICURLN-1 
      IF(NLINES.GT.0) ILSTLN = ILINE+NLINES-1 
C     ** CLOSE DCB IF AN ERROR OCCURRED -- NRV 840709 
900   IF (IERR.LT.0) CALL CLOSE(IDCBSK) 
      RETURN
      END 
