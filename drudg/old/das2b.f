C@DAS2B
C     DOUBLE PRECISION FUNCTION DAS2B(IAS,IC1,NCH,IERR)
      real*8 FUNCTION DAS2B(IAS,IC1,NCH,IERR)
C
C     THIS FUNCTION CONVERTS AN ASCII STRING TO DOUBLE PRECISION
C
C  INPUT PARAMETERS:
      integer*2 IAS(*)
      integer ic1,nch
C       INPUT STRING WITH ASCII CHARACTERS
C     IC1 - FIRST CHARACTER TO USE IN IAS
C     NCH - NUMBER OF CHARACTERS TO CONVERT

C  OUTPUT
      integer ierr
C     IERR - ERROR RETURN, 0 IF OK, -1 IF ANY CHARACTER IS NOT A NUMBER
C
C  LOCAL VARIABLES
      integer ifc,i,iec,idc,ncint,isign,iexp
C     IFC - FIRST CHARACTER WHICH IS NOT + OR -
C     IEC - LAST CHARACTER TO BE CONVERTED
C     IDC - CHARACTER NUMBER OF DECIMAL POINT
C     NCINT - NUMBER OF CHARACTERS IN INTEGER PART
C     ISIGN - +1 OR -1
      integer onine,ocape,osmalle,oplus,ominus,odot,ozero,
     .ocapd,osmalld
      real*8 VAL
C       VALUE BUILT UP DURING SCAN OF CHARACTERS
C     IEXP - EXPONENT FOR SCALING
C
      data onine/o'71'/,ocape/o'105'/,osmalle/o'145'/,oplus/o'53'/,
     .ominus/o'55'/,odot/o'56'/,ozero/o'60'/,ocapd/o'104'/,
     .osmalld/o'144'/
C
C     SET UP THE CHARACTER COUNTERS NEEDED.  FIND THE DECIMAL POINT.
C     DETERMINE THE SIGN OF THE NUMBER.
C
      IERR = 0
      IFC = IC1
      IEC = IFC + NCH - 1
      IF (JCHAR(IAS,IC1).EQ.OMINUS.OR.JCHAR(IAS,IC1).EQ.OPLUS) IFC=IFC+1
      IDC = ISCNC(IAS,IFC,IEC,ODOT)
      IF (IDC.EQ.0) IDC = IEC + 1
      NCINT = IDC - IFC
      ISIGN = +1
      IF (JCHAR(IAS,IC1).EQ.OMINUS) ISIGN = -1
C
C     CONVERT THE CHARACTERS IN THE INTEGER PART
C
      VAL = 0.D0
      IF (NCINT.EQ.0) GOTO 200
      DO 100 I=IFC,IDC-1
        IF (JCHAR(IAS,I).LT.OZERO.OR.JCHAR(IAS,I).GT.ONINE) GOTO 990
        IEXP = IDC - I - 1
      IDUM = IAS2B(IAS,I,1)
        VAL = VAL + IAS2B(IAS,I,1)*10.D0**IEXP
100     CONTINUE
C
C     CONVERT THE CHARACTERS FOLLOWING THE DECIMAL POINT
C
200   IF (IDC.GE.IEC) GOTO 980
      DO 201 I = IDC+1, IEC
        IF (JCHAR(IAS,I).EQ.OCAPD.OR.JCHAR(IAS,I).EQ.OCAPE) GOTO 300
        IF (JCHAR(IAS,I).EQ.OSMALLD.OR.JCHAR(IAS,I).EQ.OSMALLE) GOTO 300
        IF (JCHAR(IAS,I).LT.OZERO.OR.JCHAR(IAS,I).GT.ONINE) GOTO 990
        VAL = VAL + IAS2B(IAS,I,1)*10.D0**(IDC-I)
      IDUM=IAS2B(IAS,I,1)
      IEXP = IDC-I
201     CONTINUE
      GOTO 980
C
C     TAKE CARE OF THE EXPONENT FOUND AFTER THE "D" OR "E"
C
300   IEXP = IAS2B(IAS,I+1,IEC-I)
      VAL = VAL*10.D0**IEXP
C
C     FINISH UP NOW
C
980   DAS2B = VAL*ISIGN
      RETURN
C
C     HANDLE ERRORS HERE
C
990   IERR = -1
      DAS2B = 0.D0
      RETURN
      END
