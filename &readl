FTN4,X
C 
C@READL 
C 
      SUBROUTINE READL(LNEWSO,LNEWSN,LSTID),GET NEXT OBS. FROM LOG FILE 
     .C#870115:05:36# 
C 
C READL - Gets SUMMARY data from next observation from the log file.
C 
C MODIFICATIONS:
C 
C    DATE     WHO  DESCRIPTION
C    820513   KNM  SUBROUTINE CREATED 
C    820818   KNM  COMMENTS ARE WRITTEN OUT BY CALLING LXWRT
C 
C INPUT VARIABLES:
C 
      DIMENSION LNEWSO(4) 
C       - Contains new source name. 
C 
C    LSTID - Station identifier 
C    LNEWSN - A flag which indicates whether a new source has been
C             encountered.
C 
C OUTPUT VARIABLES: 
C 
C 
C COMMON BLOCKS USED: 
C 
      INCLUDE #LXCOM::LX
C 
C SUBROUTINE INTERFACES:
C 
C    CALLING ROUTINES:
C 
C      LXSUM - SUMMARY command. 
C 
C    CALLED SUBROUTINES:
C 
C      LXWRT - Writes out LOGEX data
C      LNFCH Utilities
C 
C LOCAL VARIABLES:
C 
      DIMENSION IREG(2),IPARM(2),IVAL(2)
C        - Registers for reading; parameters from GTPRM 
C        - REG, PARM - two word variables equiv 
C 
      EQUIVALENCE (REG,IREG(1)),(PARM,IPARM(1)),(VALUE,IVAL(1)) 
C 
      DIMENSION LERROR(4) 
C       - Contains the CHEKR error messages to search for.
C 
      DIMENSION LLABEL(3) 
C       - Contains the LABEL command to search for. 
C 
      DIMENSION LTRACK(4) 
C       - Contains the telescope status TRACKING. 
C 
      DIMENSION LSOUOE(5) 
C       - Contains the ONSOURCE response characters to search for.
C 
      DIMENSION LSOURE(4) 
C       - Contains the SOURCE command characters to search for in the 
C         log entries during the execution of the SUMMARY.
C 
      DIMENSION LTAPE(3)
C       - Contains the TAPE command characters to search for in the log.
C 
      DIMENSION LUNLOD(3) 
C       - Contains the UNLOD response to search for.
C 
C     ICHK - Indicates whether a CHEKR error has been encountered.
C     IET - Indicates whether an stop time has been encountered.
C     ISLEW - Indicates whether a slewing response has been encountered.
C     IST - Indicates whether a start time has been encountered.
C 
C 
C INITIALIZED VARIABLES:
C 
      INTEGER LSTART(2) 
      DATA LERROR/2HER,2HRO,2HR ,2HCH/
      DATA LLABEL/2HLA,2HBE,2HL=/ 
      DATA LTRACK/2HTR,2HAC,2HKI,2HNG/
      DATA LSOUOE/2HON,2HSO,2HUR,2HCE,2H/ / 
      DATA LSOURE/2HSO,2HUR,2HCE,2H= /
      DATA LSTART/2HST,2H= /  
      DATA LSTOP/2HET/
      DATA LTAPE/2HTA,2HPE,2H/ /
      DATA LUNLOD/2HUN,2HLO,2HD / 
C 
C 
C  ***************************************************************
C 
C  1. Initialize for a new source.
C 
C  ***************************************************************
C 
C 
      CALL IFILL(LSOURN,1,8,2H  ) 
      CALL IFILL(LSOUON,1,8,2H  ) 
      CALL IFILL(LSFT,1,4,2H  ) 
      CALL IFILL(LSFT,2,1,55B)
      CALL IFILL(LEFT,1,4,2H  ) 
      CALL IFILL(LEFT,2,1,55B)
      CALL IFILL(LSTAT,1,6,40B) 
      CALL ICHMV(LSTAT,1,2HO ,1,1)
      CALL ICHMV(LSTAT,2,2HE ,1,1)
      ISLD=0
      ISLHR=0 
      ISLMIN=0
      ISLSEC=0
      IELD=0
      IELHR=0 
      IELMIN=0
      IELSEC=0
      ILRDAY=0
      ILRHRS=0
      ILRMIN=0
      IST=-1
      IET=-1
      ITCNTL=-1 
      ISLEW=-1
      ICHK=-1 
      IF(LNEWSN.NE.2H$$) CALL ICHMV(LSOURN,1,LNEWSO,1,8)
D     WRITE(LUUSR,89) LSOURN,LNEWSO 
D89   FORMAT("LSOURN="4A2,2X,"LNEWSO="4A2/) 
C 
C 
C  *************************************************************
C 
C  2.  Call LXGET to read a log entry.
C 
C  *************************************************************
C 
C 
100   CALL LXGET
      IF(ILEN.EQ.-1) CALL ICHMV(LTAPEN,1,LTAPN,1,8) 
      IF(ICODE.EQ.-1.OR.LSTEND.EQ.-1.OR.ILEN.EQ.-1) GOTO 1200 
C 
C 
C  ************************************************************ 
C 
C  3. Check for any comments in the log. If the entry is a com- 
C     ment, write it to the terminal.  If we are in non-
C     interactive mode, write it to the output file.
C 
C  ************************************************************ 
C 
C 
      IF(ICHAR(IBUF,11).NE.42B) GOTO 300
      CALL IFILL(JBUF,1,100,40B)
      CALL IFILL(JBUF,1,1,42B)
      CALL ICHMV(JBUF,2,LSTID,1,1)
      CALL ICHMV(JBUF,4,IBUF,1,ILEN*2)
      NCOM=ILEN*2+2 
      CALL LXWRT(JBUF,NCOM) 
      GOTO 100
C 
C 
C  *********************************************************
C 
C  4. Check for a source command. If we have a new source,
C     save it and return to LXSUM to write out the data.
C 
C  *********************************************************
C 
C 
300   IF(ICHCM(IBUF,11,LSOURE,1,7).NE.0) GOTO 350 
      ICH = 18
      CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
C 
C  Make sure there are some characters in the source name 
C  before trying to move them.  Accept a maximum of 8 
C  characters.
C 
      IF(LNEWSN.NE.2H$$) GOTO 310 
      IF(ICH-19.LE.0) GOTO 100
      CALL ICHMV(LNEWSO,1,IBUF,18,MIN0(8,ICH-19)) 
      CALL ICHMV(LSOURN,1,IBUF,18,MIN0(8,ICH-19)) 
      LNEWSN=2H 
D     WRITE(LUUSR,90) LSOURN,LNEWSO 
D90   FORMAT("FIRST SOURCE LSOURN="4A2,2X,"LNEWSO="4A2/)
      GOTO 100
310   CALL IFILL(LNEWSO,1,8,2H  ) 
      IF(ICH-19.GT.0) CALL ICHMV(LNEWSO,1,IBUF,18,MIN0(8,ICH-19)) 
D     WRITE(LUUSR,91) LNEWSO
D91   FORMAT("LNEWSO="4A2/) 
      GOTO 1200 
C 
350   IF(ICHCM(LSOURN,1,8H        ,1,8).EQ.0) GOTO 100
C 
C 
C  ************************************************************ 
C 
C  5. Check for the START TIME command characters.
C 
C  ************************************************************ 
C 
C 
400   IF(ICHCM(IBUF,11,LSTART,1,3).NE.0) GOTO 500 
      ISLD=IAS2B(IBUF,1,3)
      ISLHR=IAS2B(IBUF,4,2) 
      ISLMIN=IAS2B(IBUF,6,2)
      ISLSEC=IAS2B(IBUF,8,2)
      IST=1 
D     WRITE(LUUSR,92) IST 
D92   FORMAT("IST="I3/) 
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  6. Check for the tracking status.
C 
C  ************************************************************ 
C 
C 
500   IF(ICHCM(IBUF,11,LSOUOE,1,9).NE.0) GOTO 600 
      IF(ICHCM(IBUF,20,LTRACK,1,8).NE.0) GOTO 510 
      CALL ICHMV(LSTAT,1,2HO ,1,1)
      GOTO 100
510   IF(ISLEW.EQ.1.OR.IET.EQ.1) GOTO 100 
      CALL ICHMV(LSTAT,1,2Ho ,1,1)
      ISLEW=1 
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  7. Check for any CHEKR errors. 
C 
C  *************************************************************
C 
C 
600   IF(ICHCM(IBUF,11,LERROR,1,8).NE.0) GOTO 700 
      IF(ICHK.EQ.1) GOTO 100
      CALL ICHMV(LSTAT,2,2He ,1,1)
      ICHK=1
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  8. Check for the STOP TIME command characters. 
C 
C  ************************************************************ 
C 
C 
700   IF(ICHCM(IBUF,11,LSTOP,1,2).NE.0) GOTO 800
      IELD=IAS2B(IBUF,1,3)
      IELHR=IAS2B(IBUF,4,2) 
      IELMIN=IAS2B(IBUF,6,2)
      IELSEC=IAS2B(IBUF,8,2)
      IET=1 
D     WRITE(LUUSR,93) IET 
D93   FORMAT("IET="I3/) 
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  9. Check for a LABEL command characters. 
C 
C  ************************************************************ 
C 
C 
800   IF(ICHCM(IBUF,11,LLABEL,1,6).NE.0) GOTO 900 
      CALL ICHMV(LTAPEN,1,IBUF,17,8)
      CALL ICHMV(LTAPN,1,LTAPEN,1,8)
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  10. Check for a UNLOD command. If we have one, re-initialize 
C      the tape number to "$$". 
C 
C  ************************************************************ 
C 
C 
900   IF(ICHCM(IBUF,11,LUNLOD,1,5).NE.0) GOTO 1000
      CALL ICHMV(LTAPN,1,LTAPEN,1,8)
      CALL IFILL(LTAPEN,1,8,2H  ) 
      CALL IFILL(LTAPEN,3,1,55B)
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  11. Check for tape footage.
C 
C  ************************************************************ 
C 
C 
1000  IF(ICHCM(IBUF,11,LTAPE,1,5).NE.0) GOTO 1100 
      NCH=1 
      CALL GTPRM(IBUF,NCH,NCHAR,0,PARM) 
      CALL GTPRM(IBUF,NCH,NCHAR,0,PARM,IERR)
      IF(IAS2B(PARM,1,4).EQ.-32768) GOTO 100
      IF(IST.EQ.-1) CALL ICHMV(LSFT,1,PARM,1,4) 
      IF(IET.EQ.1) CALL ICHMV(LEFT,1,PARM,1,4)
      GOTO 100
C 
C 
C  ************************************************************ 
C 
C  12. Check for the time control command. (e.g.!323113715) 
C 
C  ************************************************************ 
C 
C 
1100  IF(ICHAR(IBUF,11).NE.41B) GOTO 100
      IF(IST.EQ.1) GOTO 100 
      INUM=IAS2B(IBUF,12,3) 
      IF(INUM.EQ.-32768) GOTO 100 
      ILRDAY=INUM 
      ILRHRS=IAS2B(IBUF,15,2) 
      ILRMIN=IAS2B(IBUF,17,2) 
D     WRITE(LUUSR,23) ILRDAY,ILRHRS,ILRMIN
D23   FORMAT("ILRDAY="I3,2X,"ILRHRS="I2,2X,"ILRMIN="I2/)
      ITCNTL=1
      GOTO 100
1200  RETURN
      END 
