ASMB,R,L
* HP CONTRIB "RTE SELF-SUSPEND ROUTINE"  ## 22401-80001 
* MODIFIED FOR RTE BY ARW 770623
* MODIFIED FOR RTE-IV BY ARW 790207 
* CHECK FOR IMULT<=0     ARW 790306 
********************************************************************
* 
*   PROGRAM NAME:  SUSP   TYPE 7
* 
*   PROGRAM PURPOSE:  SELF SUSPEND FOR A GIVEN TIME.
*                     IF PROGRAM IS IN TIME LIST, IT WILL BE RESTORED 
*                     TO TIME LIST AFTER SELF-SUSPEND.
* 
*   INPUTS:  RESOLUTION CODE
*              1  TENS OF MILLISECONDS
*              2  SECONDS 
*              3  MINUTES 
*              4  HOURS 
*            MULTIPLE--THE NUMBER OF HOURS, MINUTES,ETC TO BE SUSPENDED 
*                      AS INDICATED BY THE RESOLUTION CODE. 
* 
*   OUTPUT:  NONE 
* 
*   FORTRAN CALL: 
*      CALL SUSP (IRSL,IMULT) 
* 
*                 WHERE IRSL IS THE RESOLUTION CODE AND IMULT IS THE
*                       MULTIPLE OF THE RESOLUTION CODE.
*                       E.G.  IF IMULT=20  & IRSL=1  THEN SUSPENSION
*                             TIME IS 200MSEC.
*                       IMMEDIATE RETURN IF IMULT<=0
* 
*   ASSEMBLER CALL: 
* 
*      JSB SUSP 
*      DEF *+3
*      DEF IRSL 
*      DEF IMULT
*      <RETURN POINT> 
* 
*      WHERE IRSL AND IMULT CONTAIN THE RESOLUTION CODE AND 
*      MULTIPLE AS DESCRIBED ABOVE. 
      NAM SUSP,7 Self-suspend routine   ARW 790306
      EXT EXEC,.ENTR,TMVAL,$OPSY
      ENT SUSP
RESLC NOP 
OFSTO NOP 
SUSP  NOP           ENTRY POINT 
      JSB .ENTR     TRANSFER IN PARAMS. 
      DEF RESLC 
      LDA OFSTO,I 
      SZA,RSS       RETURN IF IMULT<=0
      JMP SUSP,I
      SSA 
      JMP SUSP,I
      CMA,INA       NEGATE THE VALUE OF THE TIME FOR LATER
      STA OFST       EXEC USE.
      LDB 1717B     GET ID SEGMENT ADDRESS
      ADB =D17      RESL AND TIME PARAMS START HERE IN ID18 
      LDA OPSY,I    GET TYPE OF OP SYS
      RAR 
      SLA           DMS IN THIS SYSTEM? 
      JMP DMS        YES. MUST USE CROSS LOADS. 
      LDA B,I        NO.
      STA RESL
      INB           GET ADDRESS OF ID WORD 19 
      LDA B,I 
      JMP SUSP1 
DMS   XLA B,I 
      STA RESL
      INB 
      XLA B,I 
SUSP1 STA TIME
      JSB TMVAL     DECODE TIME OF NEXT EXECUTION 
      DEF *+3 
      DEF TIME
      DEF TMSEC 
      JSB EXEC      SUSPEND SELF FOR TIME 
      DEF *+6       TRANSFERED IN.
      DEF RCODE 
      DEF PROG
      DEF RESLC,I 
      DEF PROG
      DEF OFST
      LDA RESL      WAS PROG IN THE T LIST? 
      AND =B10000 
      SZA,RSS 
      JMP SUSP,I    NO
      XOR RESL      YES, GET THE PROGS
      CLB           RESOLUTION CODE AND 
      RRL 3         MULTIPLE. 
      ARS,ARS 
      ARS 
      STB RC
      STA MT
      JSB EXEC      RESCHEDULE THE PROG.
      DEF *+9       AT THE ORIGINAL TIME. 
      DEF RCODE 
      DEF PROG
      DEF RC
      DEF MT
      DEF HRS 
      DEF MIN 
      DEF SEC 
      DEF TMSEC 
      JMP SUSP,I
OPSY  DEF $OPSY 
TIME  NOP 
RCODE DEC 12
PROG  NOP 
RC    NOP 
MT    NOP 
RESL  NOP 
TMSEC NOP 
SEC   NOP 
MIN   NOP 
HRS   NOP 
DAYS  NOP 
OFST  NOP 
A     EQU 0 
B     EQU 1 
      END 
