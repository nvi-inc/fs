FTN77,I,Y
$CDS ON
      PROGRAM MOON(3),GET MOON POSITION
C
      DOUBLE PRECISION CONV,UTC,UTFRAC,RA,DEC,DAYS,
     .DASIN,QUAD,X,DATTIM,RANGE,GST,STM,FRAC,
     .REARTH,XM,YM,ZM,SINGST,COSGST,XME,YME,ZME,XSM,YSM,ZSM,
     .RSM,DIAM,HAD,EL,AZ,GDLAT,GDLON,HITE,XS,YS,ZS,RATE,GDHEI
C
C  INPUT VARIABLES
C
      DIMENSION IBUFI(18)
C
C  OUTPUT VARIABLES
C
      DIMENSION IBUFO( 8)
C
C  LOCAL VARIABLES
C
      DIMENSION IT(6)
C
      INCLUDE /FS/INCLUDE/DPI.FTNI
C
C  INCOMING  BUFFER  18 WORDS
C
      EQUIVALENCE (IBUFI(1),GDLAT),
     +            (IBUFI(5),GDLON),
     +            (IBUFI(9),GDHEI),
     +            (IBUFI(13),IT)
C
C  OUTGOING BUFFER 8 WORDS
C
      EQUIVALENCE (IBUFO(1),RA),
     +            (IBUFO(5),DEC)
C
C INITIALIZED VARIABLES
      DATA REARTH,RATE /6378160D0,1.002737909D0/
C
C   HISTORY:
C  DATE   WHO  WHAT
C 841120  MWH  CREATED
C 850426  WEH  fixed precision of HEIGHT argument to GD2XY
C              add seconds to UTFRAC
C
C Statement function for double precision arcsin
      DASIN(X) = DATN2(X,DSQRT(DABS(1D0-X*X)))
C
C  1. Get command, initialize date/time info
C
      CALL EXEC(14,1,IBUFI,24)
D     WRITE(1,999) GDLAT,GDLON,GDHEI,IT
D999  FORMAT(" GDLAT,GDLON,GDHEI, IT"/
D    +        3F20.15/6I5)
C
      CALL GD2XY(GDLON,GDLAT,GDHEI ,XS,YS,ZS)
D     WRITE(1,9941) XS,YS,ZS
D9941  FORMAT(" XS,YS,ZS "/3D18.12)
      SINPHI = DSIN(GDLAT)
      COSPHI = DCOS(GDLAT)
      IY = IT(6)-1900
      JD = JULDA(1,IT(5),IY)
      UTFRAC = (IT(2)+60D0*IT(3)+3600D0*IT(4))/86400.D0
      UTC = UTFRAC*DTWOPI
      CALL SIDTM(JD,STM,FRAC)
      GST = STM + UTC*RATE
      DATTIM = JD + 2439999.5D0 + UTFRAC
      CALL MOONP(DATTIM,RA,DEC,RANGE)
      RA = RA * DTWOPI /24D0
      RA = DMOD(RA,DTWOPI)
      IF (RA.LT.0D0) RA = RA + DTWOPI
      DEC = DEC * DEG2RAD
      RANGE = RANGE * REARTH
      XM = RANGE * DCOS(DEC) * DCOS(RA)
      YM = XM * DTAN(RA)
      ZM = RANGE * DSIN(DEC)
D     WRITE(1,9901) RA,DEC
D9901 FORMAT("RA,DEC FROM MOONP = "2D20.15)
      SINGST = DSIN(GST)
      COSGST = DCOS(GST)
      XME = XM * COSGST + YM * SINGST
      YME = -XM * SINGST + YM * COSGST
      ZME = ZM
D     WRITE(1,9946) GST,XME,YME,ZME
D9946  FORMAT(" GST,XME,YME,ZME "/4D18.12)
C
C  Get vector from station to moon
C
      XSM = XME - XS
      YSM = YME - YS
      ZSM = ZME - ZS
      RSM = DSQRT(XSM*XSM + YSM*YSM + ZSM*ZSM)
      DIAM = 3.476D6 / RSM
      DIAM = DIAM * RAD2DEG
D     WRITE(1,9947) XSM,YSM,ZSM,RSM
D9947  FORMAT(" XSM,YSM,ZSM,RSM "/4D18.12)
C
C  Get apparent HA and DEC from station-moon vector and AZ and
C   EL from HA and DEC
C
      DEC = DASIN(ZSM / RSM)
      HAD = GDLON - DATN2(YSM,XSM)
      HAD = DMOD(HAD,DTWOPI)
      IF (HAD.GT.DPI) HAD = HAD - DTWOPI
      IF (HAD.LT.-DPI) HAD = HAD + DTWOPI
      RA = GST + GDLON - HAD
      RA = DMOD(RA,DTWOPI)
      IF (RA.LT.0D0) RA = RA + DTWOPI
C
D     WRITE(1,998) RA,DEC
D998  FORMAT(" RA, DEC, ",2F20.15)
      CALL EXEC(14,2,IBUFO,12)
C
      END
