FTN77,Y,I  
$CDS ON
$ALIAS /FSCOM/ , NOALLOCATE
$ALIAS RNRQ, NOABORT
      SUBROUTINE LVDONN(LOCK,IP)
      CHARACTER*(*) LOCK
      INTEGER*2 IP(5)
C
C  LVDONN: turn LVDT on & wait to stablize
C
C  INPUT:
C     LOCK: if "LOCK" then lock the LVDT RN before accessing
C           if "UNLOCK" then unlock the LVDT RN after accessing
C
C  OUTPUT:
C     IP - Field System Return Parameters
C     IP(3) - 0 if no error
C
      INCLUDE /fs/include/fscom.FTNI
C
      INTEGER*2 IBUF(6),ICLASS,NREC
C
      IF(LOCK.EQ.'LOCK') THEN
        CALL RNRQ(40001B,IRNLVDT_FS,ISTAT,*10)
10      CONTINUE
        IF(ISTAT.NE.2) THEN
          IP(3)=-408
          IP(4)=2HQ@
          RETURN
        ENDIF
      ENDIF
C
      NREC=0
      ICLASS=0
C
      IBUF(1)=0
      IBUF(2)=2HHD
      CALL AD2MA(IBUF(3),0,0,1)
      CALL ADD_CLASS(IBUF,-12,ICLASS,NREC)
C
C   SEND STROBE
C
      IBUF(1)=5
      IBUF(2)=2H%
      CALL ADD_CLASS(IBUF,-3,ICLASS,NREC)
      KLVDT_FS=.TRUE.
C
C   Now schedule MATCN
C
      CALL RUN_MATCN(ICLASS,NREC,IP)
      CALL CLRCL(IP(1))
      IP(2)=0
C
C  WAIT 0.25 SECONDS TO ALLOW LVDT TO STABILIZE
C
      IF(IP(3).EQ.0) THEN
        CALL EXEC(12,0,1,0,-25)
      ENDIF
C
      IF(LOCK.EQ.'UNLOCK'.OR.IP(3).NE.0) THEN
        CALL RNRQ(00004B,IRNLVDT_FS,ISTAT,*90)
90      CONTINUE
        IF(ISTAT.NE.1) THEN
          IF(IP(3).NE.0) CALL LOGIT(0,0,0,0,IP(3),IP(4),IP(5))
          IP(3)=-409
          IP(4)=2HQ@
        ENDIF
      ENDIF
C
      RETURN
      END
