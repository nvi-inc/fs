FTN77,I,Y
$CDS ON
        REAL FUNCTION GBFAS(IBUF,ISTRT,IEND)
     .,C#870407:12:15    BINARY TO ASCII# 
C 
C           This routine will return an integer found starting at 
C    ISTRT.  The integeter will be terminated when this routine either
C    reaches IEND, or comes to the first non integer character after
C    finding an integer or sign.  The number found is returned in the 
C    A reg (IRGA) with the count in the B reg (IRGB).  The returned 
C    information should be studied in the write up which follows. 
C 
C 
C     INPUT PARAMETERS: 
C 
        DIMENSION IBUF(1),IREG(2) 
C 
        LOGICAL KNUMF 
C 
C       IBUF    - the input buffer
C 
C       ISTRT   - the first character in IBUF to start with 
C 
C       IEND    - the last character to be used in IBUF 
C 
C 
C      OUTPUT PARAMETERS: 
C 
C 
C       GBFAS  -  with the EQUIVALENCE (GBFAS,IRGA,IREG(1)),(IRGB,IREG(2))
C 
C        IRGA   - the integer found starting at ISTRT and as given
C                 by the ISTAT part of IRGB 
C 
C        IRGB   =  ISTAT*400B+ICNT
C 
C             ICNT  -   the count of the charcters used in the return 
C                       of the rest of the information from this call 
C 
C             ISTAT -  0  normal return (i. e. IRGA ia valid and in the 
C                         range -32768 <= IRGA <= 32767)
C 
C                      1  overflow occurred in the value of IRGA if the 
C                         next character were used.  IRGA ia valid for
C                         the number of characters used so far. 
C 
C                       2  only blanks found before non numeric.
C 
C                       3  only blanks and sign found before non numeric
C                          IRGA = s1 (i. e. +1/-1)
C 
C                       4  255 blanks have been found 
C 
C                       5  ISTRT was greater than IEND upon entry.
C 
C       BUFFER FORMAT ACCEPTED: 
C 
C              bbbbbsbbbnnnnnnnnc 
C 
C             1.  any number of blanks may be before or after sign s s = +/-
C 
C             2.  any number of zeros may preceed the number
C 
C             3.  any non-nummeric character will terminate the conversion
C                 as well as the reasching of IEND. 
C 
C             4.  a check should be made of IRGB upon return since
C                 the format  bbbbsbbbc is accepted also. 
C 
C 
        EQUIVALENCE (REG,IRGA,IREG(1)),(IRGB,IREG(2)) 
C 
C 
C               INITIALIZE INTERNAL PARAMETERS
C 
        KNUMF =.FALSE.
C 
        ISIGN=0 
C 
        IX=0
C 
C               CHECK FOR VALID CALL
C 
        IF(ISTRT .GT. IEND)GO TO 98500
C 
        IRGA=0
        IRGB=0
C 
C               LOOP THROUGH THE CHARACTERS 
C 
        DO 70000 IX=ISTRT,IEND
C 
C               SAVE CURRENT INDEX FOR LOOP LEAVING 
C 
        I=IX
C 
C               GET THE CURRENT CHARACTER TO LOOK AT
C 
        IC=JCHAR(IBUF,I)
C 
C               SEE IF IC IS A NUMBER 
C 
        IF(IC .LT. 60B .OR. 71B .LT. IC)GO TO 10000 
C 
C               IC IS A NUMBER
C 
C               CONVERT IC TO AN INTEGER IN BINARY
C 
        IC=IC-60B 
C 
C               NOTE A DIGIT HAS BEEN FOUND 
C 
        KNUMF= .TRUE. 
C 
C               ASSIGN A SIGN IF NONE FOUND YET 
C 
        IF(ISIGN .EQ. 0)ISIGN=1 
C 
C               CHECK IRGA WILL NOT OVERFLOW
C 
        IF(IABS(IRGA) .GT. 3277)GO TO 98100 
C 
C               CHECK FOR THE NEARLY OVERFLOW CASE LEFT 
C 
        IF(             IABS(IRGA) .EQ. 3276
     C          .AND.  (       (        ISIGN .EQ. 1
     C                          .AND.   IC .GT. 7       ) 
     C                  .OR.   (        ISIGN .EQ. -1 
     C                          .AND.   IC .GT. 8       )  )
     C          )GO TO 98100
C 
C               COMPUTE NEW VALUE - IT IS NOW SAFE
C 
        IRGA=10*IRGA+ISIGN*IC 
C 
C               GO GET THE NEXT CHARACTER IF ANY
C 
        GO TO 70000 
C 
10000   CONTINUE
C 
C               HAVE ANY CHARACTERS BEEN FOUND YET
C 
        IF( .NOT. KNUMF)GO TO 20000 
C 
C               THE NORMAL FINISH CONDITION HAS OCCURRED
C 
        IRGB=I-ISTRT+1
C 
C               NOW NNORMAL EXIT
C 
        GO TO 99000 
C 
20000   CONTINUE
C 
C               IF THE CURRENT CHARACTER A BLANK
C 
        IF(IC .NE. 40B)GO TO 30000
C 
C               CHECK FOR TOO MANY BLANKS SO FAR
C 
        IF(I-ISTRT .GE. 255)GO TO 98400 
C 
C               NOT TOO MANY BLANKS YET CONTINUE
C 
        GO TO 70000 
C 
30000   CONTINUE
C 
C               HAS A SIGN BEEN FOUND YET 
C 
        IF(ISIGN .EQ. 0)GO TO 40000 
C 
C               HERE WE HAVE    1. SIGN 
C                               2. NO BLANK 
C                               3. NO NUMERIC YET 
C 
        GO TO 98300 
C 
40000   CONTINUE
C 
C               CHECK FOR A SIGN NOW
C 
        IF(IC .EQ. 53B)ISIGN=1
C 
        IF(IC .EQ. 55B)ISIGN=-1 
C 
C               IF SIGN NOT FOUND THE LAST CASE IS EXHAUSTED
C 
        IF(ISIGN .EQ. 0)GO TO 98200 
C 
70000   CONTINUE
C 
C               INCREMENT I IN CASE OF ERROR - FOR PROPER COUNT 
C 
        I=I+1 
C 
C               CHECK FOR INFORMATION FOUND 
C 
        IF(ISIGN .EQ. 0)GO TO 98200 
C 
C               CHECK MORE THAN SIGN WAS FOUND
C 
        IF( .NOT. KNUMF)GO TO 98300 
C 
C               THE NORMAL COMPLETE  EXIT ROUTE 
C 
        IRGB=IEND-ISTRT+1 
C 
C               GO BACK 
C 
        GO TO 99000 
C 
C       ********************************* 
C       *                               * 
C       *       ERROR EXITS             * 
C       *                               * 
C       ********************************* 
C 
98100   CONTINUE
C 
C               OVERFLOW
C 
        IRGB=400B+I-ISTRT 
C 
        GO TO 99000 
C 
98200   CONTINUE
C 
C               ONLY BLANKS BEFORE NON-NUMERIC OR END 
C 
        IRGB=1000B+I-ISTRT
C 
        GO TO 99000 
C 
98300   CONTINUE
C 
C               ONLY BLANKS AND SIGN BEFORE NON-NUMERIC OR END
C 
        IRGB=1400B+I-ISTRT
C 
C               RETURN SIGN VALUE IN IRGA 
C 
        IRGA=ISIGN
C 
        GO TO 99000 
C 
98400   CONTINUE
C 
C               255 BLANKS (WILL THIS EVER OCCUR ?) 
C 
        IRGB=2000B+I-ISTRT+1
C 
        GO TO 99000 
C 
98500   CONTINUE
C 
C               ISTRT AFTER IEND
C 
        IRGB=2400B
C 
99000   CONTINUE
C 
C               RETURN INFORMATION FOUND
C 
        GBFAS=REG 
C 
        RETURN
C 
        END 
        END$
