ASMB
      NAM NARG,7  GET # OF CALLING ARGUMENTS    ARW 781015
      ENT NARG
      MIC MPX,102000B,0 MP ABEND IF LOCATION EXECUTED 
* 
*When called as the FIRST EXECUTABLE STATEMENT in a host FORTRAN
*routine, returns # of arguments in call to host routine. 
* 
*     NARGS=NARG(IDUMY) 
*            where IDUMY= dummy argument
*                  NARGS= # of arguments in call to host routine
*                  B-reg return: # params in host routine definition. 
* 
*     ARW 781015
* 
IARG  NOP 
NARG  NOP 
      STA SAVE
      ISZ NARG
      ISZ NARG      SET RETURN ADDRESS
      CLA 
      STA IARG
LOOP  LDA B,I 
      CPA SAVE      B,I SAME AS A-REG?
      JMP OUT        YES. 
      CPA =B015223  (A-reg)=JSB .ENTR?
      MPX             YES. SHOULD NEVER GET HERE. CREATE MEM PROT ERROR 
      INB 
      ISZ IARG
      JMP LOOP
OUT   STB SAVE      (B-reg)=ENTRY POINT ADDRESS OF HOST 
      INB 
      INB 
      LDB B,I       GET ADDR OF DEF OF 1st PARM IN HOST ROUTINE DEFINITION  
      CMB,INB 
      ADB SAVE      (B-reg)=# PARAMS IN HOST ROUTINE DEFINITION 
      LDA IARG
      CMA,INA 
      ADA B         (A-reg)=# OF ARGUMENTS IN CALL TO HOST ROUTINE
      JMP NARG,I
* 
A     EQU 0 
B     EQU 1 
SAVE  BSS 1 
      END 
