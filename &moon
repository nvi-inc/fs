FTN4,X
C@MOON
      PROGRAM MOON(3),GET MOON POSITION 
C 
      DOUBLE PRECISION PIE,TWOPI,CONV,UTC,UTFRAC,RA,DEC,DAYS, 
     .DASIN,QUAD,X,DATTIM,RANGE,GST,STM,FRAC, 
     .REARTH,XM,YM,ZM,SINGST,COSGST,XME,YME,ZME,XSM,YSM,ZSM,
     .RSM,DIAM,HAD,EL,AZ,GDLAT,GDLON,HITE,XS,YS,ZS,RATE,GDHEI 
C 
C  INPUT VARIABLES
C 
      DIMENSION IBUFI(24) 
C 
C  OUTPUT VARIABLES 
C 
      DIMENSION IBUFO(12) 
C 
C  LOCAL VARIABLES
C 
      DIMENSION IT(6) 
C 
C  INCOMING  BUFFER  24 WORDS 
C 
      EQUIVALENCE (IBUFI(1),GDLAT), 
     +            (IBUFI(7),GDLON), 
     +            (IBUFI(13),GDHEI),
     +            (IBUFI(19),IT)
C 
C  OUTGOING BUFFER 12 WORDS 
C 
      EQUIVALENCE (IBUFO(1),RA),
     +            (IBUFO(7),DEC)
C 
C INITIALIZED VARIABLES 
      DATA PIE /3.14159265D0/ 
      DATA REARTH,RATE /6378160D0,1.002737909D0/
C 
C   HISTORY:
C  DATE   WHO  WHAT 
C 841120  MWH  CREATED
C 850426  WEH  fixed precision of HEIGHT argument to GD2XY
C              add seconds to UTFRAC
C 
C Statement function for double precision arcsin
      DASIN(X) = DATN2(X,DSQRT(DABS(1D0-X*X)))
C 
C  1. Get command, initialize date/time info
C 
      CALL EXEC(14,1,IBUFI,24)
D     WRITE(1,999) GDLAT,GDLON,GDHEI,IT 
D999  FORMAT(" GDLAT,GDLON,GDHEI, IT"/
D    +        3F10.4,5I5) 
C 
      TWOPI = 2D0*PIE 
      CONV = TWOPI/360D0
      CALL GD2XY(GDLON,GDLAT,GDHEI ,XS,YS,ZS) 
      SINPHI = DSIN(GDLAT)
      COSPHI = DCOS(GDLAT)
      IY = IT(6)-1900 
      JD = JULDA(1,IT(5),IY)
      UTFRAC = (IT(2)+60D0*IT(3)+3600D0*IT(4))/86400.D0 
      UTC = UTFRAC*TWOPI
      CALL SIDTM(JD,STM,FRAC) 
      GST = STM + UTC*RATE
      DATTIM = JD + 2439999.5D0 + UTFRAC
      CALL MOONP(DATTIM,RA,DEC,RANGE) 
      RA = RA * TWOPI /24D0 
      RA = DMOD(RA,TWOPI) 
      IF (RA.LT.0D0) RA = RA + TWOPI
      DEC = DEC * CONV
      RANGE = RANGE * REARTH
      XM = RANGE * DCOS(DEC) * DCOS(RA) 
      YM = XM * DTAN(RA)
      ZM = RANGE * DSIN(DEC)
D     WRITE(LU,9901) RA,DEC 
D9901 FORMAT("RA,DEC FROM MOONP = "2D10.4)
      SINGST = DSIN(GST)
      COSGST = DCOS(GST)
      XME = XM * COSGST + YM * SINGST 
      YME = -XM * SINGST + YM * COSGST
      ZME = ZM
C 
C  Get vector from station to moon
C 
      XSM = XME - XS
      YSM = YME - YS
      ZSM = ZME - ZS
      RSM = DSQRT(XSM*XSM + YSM*YSM + ZSM*ZSM)
      DIAM = 3.476D6 / RSM
      DIAM = DIAM / CONV
C 
C  Get apparent HA and DEC from station-moon vector and AZ and
C   EL from HA and DEC
C 
      DEC = DASIN(ZSM / RSM)
      HAD = GDLON - DATN2(YSM,XSM)
      HAD = DMOD(HAD,TWOPI) 
      IF (HAD.GT.PIE) HAD = HAD - TWOPI 
      IF (HAD.LT.-PIE) HAD = HAD + TWOPI
      RA = GST + GDLON - HAD
      RA = DMOD(RA,TWOPI) 
      IF (RA.LT.0D0) RA = RA + TWOPI
C 
D     WRITE(1,998) RA,DEC 
D998  FORMAT(" RA, DEC, ",2F10.5) 
      CALL EXEC(14,2,IBUFO,12)
C 
      END 
