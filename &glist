FTN4
      SUBROUTINE GLIST(LU,IDCB,ILFILE,ILSC,ILCR,LIST,MLIST,NLIST, 
     +           IBUF,ILEN,IERR,IDCBS)
C 
C   GET STAR LIST FOR TR-85 ANTCN 
C 
C   WEH 840620
C 
C  INPUT: 
C 
C      LU - OPERATOR LU 
C 
      DIMENSION IDCB(IDCBS) 
C 
C      IDCB - PASSED DATA CONTROL BLOCK 
C 
      DIMENSION ILFILE(3) 
C 
C      ILFILE - LIST FILE NAME
C 
C      ILSC - LIST FILE SECURITY CODE 
C 
C      ILCR - LIST CARTRIDGE REFERENCE
C 
C      MLIST  - MAXIMUM LIST ENTRIES
C 
C      IBUF   - WORKING BUFFER
C 
C      ILEN   - WORDS IN IBUF 
C 
C      IDCBS  - WORDS IN IDCB 
C 
C  OUTPUT:
C 
      DIMENSION LIST(5,MLIST) 
C 
C 
C      LIST - LIST OF SOURCES 
C 
C      NLIST - ENTRIES FOUND
C 
C      IERR - ERROR CODE
C 
C           = 0  NO ERROR 
C           = -1 ERROR
C 
C LOCAL VARIABLES:
C 
      DIMENSION LS(8) 
C 
      QDATA LOPER $ERROR eeee OPENING _$
      QDATA LEFER $NO END-OF-FILE RECORD _$ 
      QDATA LRDER $ERROR eeee READING _$
      QDATA LMXER $EXCEEDED MAXIMUM OF mmm STARS _$ 
      QDATA LCTER $MORE THAN 10 CHARACTERS IN nnnnnnnnnnnnnnnn _$ 
      QDATA LDPER $STAR nnnnnnnnnn is DUPLICATED _$ 
      QDATA LFLER $in nnnnnn STAR FILE$ 
C 
D     WRITE(LU,991) LU,ILFILE,ILSC,ILCR,MLIST,ILEN,IDCBS
D991  FORMAT(" GLIST: LU,ILFILE,ILSC,ILCR,MLIST,ILEN,IDCBS,"/ 
D    +       I5,2X,3A2,2X,I5,2X,I5,2X,I5,2X,I5,2X,I5 )
C 
C    OPEN FILE
C 
      CALL OPEN(IDCB,IERR,ILFILE,0,ILSC,ILCR,IDCBS) 
      IF(IERR.LT.0) GO TO 900 
C 
C    INITIALIZE 
C 
      NLIST=0 
C 
C    READ EACH RECORD, SKIP RECORDS THAT START WITH STAR "*"
C 
10    CONTINUE
      CALL READF(IDCB,IERR,IBUF,ILEN,IL)    
      IF(IL.EQ.-1) GO TO 910
      IF(IERR.NE.0) GO TO 920 
      IF(IL.EQ.0) GO TO 10
C 
      IF(ICHCM(IBUF,1,2H* ,1,1).EQ.0) GO TO 10
C 
C  NO STAR, BUT CHECK FOR END RECORD    
C 
      IFC=1 
      ILC=IL*2
      CALL GTFLD(IBUF,IFC,ILC,IC1,IC2)
      IF(IC1.LE.0) GO TO 10 
      IF(ICHCM(IBUF,IC1,12HEND-OF-FILE ,1,11).EQ.0) GO TO 10000 
C 
C NOT END OF FILE RECORD, CHECK TO SEE IF ITS A DUPLICATE 
C 
      IF(NLIST.GE.MLIST) GO TO 930
      INC=IC2-IC1+1 
      IF(INC.GT.10) GO TO 935 
      CALL ICHMV(LS,1,10H            ,1,10) 
      CALL ICHMV(LS,1,IBUF,IC1,INC) 
      IPOS=INDX(LIST,MLIST,NLIST,LS,10) 
      IF(IPOS.GT.0.AND.IPOS.LE.NLIST) GO TO 940 
C 
C  ITS OKAY, ADD IT TO THE LIST 
C 
      NLIST=NLIST+1 
      CALL ICHMV(LIST(1,NLIST),1,LS,1,10) 
      GO TO 10
C 
C  ERRORS 
C 
900   CONTINUE
      IC=IB2AS(IERR,LOPER(2),7,4) 
      CALL EXEC(2,LU,LOPER(2),-LOPER(1))
      GO TO 9000
C 
910   CONTINUE
      CALL EXEC(2,LU,LEFER(2),-LEFER(1))
      GO TO 9000
C 
920   CONTINUE
      IC=IB2AS(IERR,LRDER(2),7,4) 
      CALL EXEC(2,LU,LRDER(2),-LRDER(1))
      GO TO 9000
C 
930   CONTINUE
      IC=IB2AS(MLIST,LMXER(2),21,3) 
      CALL EXEC(2,LU,LMXER(2),-LMXER(1))
      GO TO 9000
C 
935   CONTINUE
      INC=MIN0(15,INC)
      IC=ICHMV(LCTER(2),28,IBUF,IC1,INC)
      IF(INC.LT.15) CALL ICHMV(LCTER(2),IC,16H                 ,  
     +                         1,15-INC)
      CALL EXEC(2,LU,LCTER(2),-LCTER(1))
      GO TO 9000
940   CONTINUE
      IC=ICHMV(LDPER(2),6,LS,1,10)
      CALL EXEC(2,LU,LDPER(2),-LDPER(1))
      GO TO 9000
C 
9000  CONTINUE
      CALL ICHMV(LFLER(2),4,ILFILE,1,6) 
      CALL EXEC(2,LU,LFLER(2),-LFLER(1))
      IERR=-1 
      GO TO 10000 
C 
10000 CONTINUE
      CALL CLOSE(IDCB)
D     WRITE(LU,992) NLIST,IERR  
D992  FORMAT(" GLIST: NLIST, IERR ",2I7)
D     IF(NLIST.GT.0) WRITE(LU,993) ((LIST(I,J),I=1,5),J=1,NLIST)
D993  FORMAT(" GLIST: LIST ENTRY: ",5A2/) 
      RETURN
      END 
