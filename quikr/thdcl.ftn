FTN77,I,Y
$ALIAS /FSCOM/ , NOALLOCATE
$CDS ON
      SUBROUTINE THDCL(IP,IPS,IHEAD,FSTFW,SLWFW,FSTRV,SLWRV)
C
      INCLUDE /FS/INCLUDE/FSCOM.FTNI
C
C Starting in centered position, THDCL moves the head block in both
C  directions at both fast and slow speed, to calibrate the speeds.
C
C  INPUT VARIABLES:
      DIMENSION IP(1),FSTFW(2),SLWFW(2),FSTRV(2),SLWRV(2)
C
C  LOCAL VARIABLES:
      LOGICAL NEW
C
C  OUTPUT VARIABLES:
C     IP - RMPAR variables from MATCN
C    POS - UPDATED HEAD POSITION(S) 
C FSTFW - UPDATED FAST FORWARD SPEED(S) 
C SLWFW - UPDATED SLOW FORWARD SPEED(S) 
C FSTRV - UPDATED FAST REVERSE SPEED(S)
C SLWRV - UPDATED SLOW REVERSE SPEED(S)
C
C  HISTORY:
C
C  DATE  WHO  WHAT
C 850104 MWH  CREATED
C 880301 LAR  ADD PASS NUMBER (IPS) TO CALLING SEQUENCES
C
C  1. First calibrate fast forward speed.
C
      NEW = .TRUE.
      DO I=1,2
        IF (IHEAD.EQ.3 .OR. IHEAD.EQ.I) THEN
          CALL POSIT(I,IPS,PNOW,IP,NEW)
          PSAVE = PNOW
          CALL HMOVE(I,1,1,2500,IP)
C         - move fast forward for 0.1 second
          IF (IP(3).LT.0) RETURN
          CALL SUSP(1,15)
          CALL POSIT(I,IPS,PNOW,IP,NEW)
          IF (IP(3).LT.0) RETURN
          FSTFW(I) = ABS((PNOW-PSAVE)/0.1)
C
C  2. Next calibrate fast reverse speed.
C
          PSAVE = PNOW
          CALL HMOVE(I,1,0,2500,IP)
C        - move fast reverse for 0.1 second
          IF (IP(3).LT.0) RETURN
          CALL SUSP(1,15)
          CALL POSIT(I,IPS,PNOW,IP,NEW)
          IF (IP(3).LT.0) RETURN
          FSTRV(I) = ABS((PNOW-PSAVE)/0.1)
C
C  3. Now check out slow forward.
C
          PSAVE = PNOW
          CALL HMOVE(I,0,1,25000,IP)
          IF (IP(3).LT.0) RETURN
          CALL SUSP(2,1)
          CALL SUSP(1,5)
          CALL POSIT(I,IPS,PNOW,IP,NEW)
          IF (IP(3).LT.0) RETURN
          SLWFW(I) = ABS((PNOW-PSAVE)/1.0)
C
C  4. Finally check slow reverse.
C
          PSAVE = PNOW
          CALL HMOVE(I,0,0,25000,IP)
          IF (IP(3).LT.0) RETURN
          CALL SUSP(2,1)
          CALL SUSP(1,5)
          CALL POSIT(I,IPS,PNOW,IP,NEW)
          IF (IP(3).LT.0) RETURN
          SLWRV(I) = ABS((PNOW-PSAVE)/1.0)
D         WRITE(LU,9001)I,FSTFW(I),FSTRV(I),SLWFW(I),SLWRV(I)
D9001     FORMAT("FF,FR,SF,SR FOR HEAD "I3" = "4F10.4)
        ENDIF
      ENDDO
      RETURN
      END 
