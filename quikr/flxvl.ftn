FTN77,I,Y
$CDS ON
      REAL*4 FUNCTION FLXVL(IMDL,ARR,BM,CORR)
      IMPLICIT NONE
      INTEGER*2 IMDL
      REAL*4 ARR(6),BM,CORR
C
C   FLXVL CALCULATES APPARENT FLUX OF A SOURCE BASED ON A SIMPLE MODEL
C
C     SOURCE MODEL: IMDL = 1 = CONCENTRIC ELLIPTICAL GAUSSIAN, (two)
C                          2 = CIRCULAR DISK
C                          3 = TWO POINTS
C
C     FLUX AND COMPONENT SIZES: ARR
C
C                  MODEL
C            1       2     3
C  E  1    FLUX1   FLUX  TFLUX
C  L  2    MAJOR1  DIAM  SEPER
C  E  3    MINOR1    -     -
C  M  4    FLUX2     -     -
C  E  5    MAJOR2    -     -
C  N  6    MINOR2    -     -
C  T
C
C  FLXVL RETURNS THE APPARENT FLUX
C  CORR  RETURNS THE `EFFECT' CORRECTION OVER NO RESOLUTION
C
      IF(IMDL.EQ.1) THEN
        FLXVL=(ARR(1)/SQRT((1.+(ARR(2)/BM)**2)*(1.+(ARR(3)/BM)**2)))
     &       +(ARR(4)/SQRT((1.+(ARR(5)/BM)**2)*(1.+(ARR(6)/BM)**2)))
        CORR=(ARR(1)+ARR(4))/FLXVL
      ELSE IF(IMDL.EQ.2) THEN
       CORR=(ALOG(2.)*(ARR(2)/BM)**2)/(1.-EXP(-ALOG(2.)*(ARR(2)/BM)**2))
       FLXVL=ARR(1)/CORR
      ELSE IF(IMDL.EQ.3) THEN
        CORR=EXP(4.*ALOG(2.)*((ARR(2)/2.)/BM)**2)
        FLXVL=ARR(1)/CORR
      ELSE
        CORR=1.0
        FLXVL=0.0
      ENDIF
C
      RETURN
      END
