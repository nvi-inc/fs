FTN77,I,Y
$ALIAS /FSCOM/ , NOALLOCATE
$CDS ON
C@SUN 
      SUBROUTINE SUN(RA,DEC,IT) 
C 
      DOUBLE PRECISION PIE,TWOPI,CONV,UTC,UTFRAC,RA,DEC,DAYS,SLON,
     .SANOM,ECLLON,DASIN,QUAD,OBLIQ,X 
C 
      DIMENSION IT(6) 
C 
C INITIALIZED VARIABLES 
      DATA PIE /3.14159265D0/ 
C 
C   HISTORY:
C  DATE   WHO  WHAT 
C 841120  MWH  CREATED
C 850426  WEH  GET JULIAN DATE RIGHT, ADD SECONDS TO UTFRAC 
C 
C Statement function for double precision arcsin
      DASIN(X) = DATN2(X,DSQRT(DABS(1D0-X*X)))
C 
C  1. Get command, initialize date/time info
C 
      TWOPI = 2D0*PIE 
      CONV = TWOPI/360D0
C 
      IY = IT(6)-1900 
      JD = JULDA(1,IT(5),IY)
      UTFRAC = (IT(2)+60D0*IT(3)+3600D0*IT(4))/86400.D0 
      UTC = UTFRAC*TWOPI
C 
C  2. Compute position of the sun 
C 
C  # of days since J2000 (=JD11545) 
C 
      DAYS = JD-11545.5D0+UTC/TWOPI 
C 
C  Mean solar longitude 
C 
      SLON = 280.46D0 + .9856474D0 * DAYS 
      SLON = DMOD(SLON,360D0) 
      IF (SLON.LT.0D0) SLON = SLON + 360D0
C 
C  Mean anomaly of the sun
C 
      SANOM = 357.528D0 + .9856003D0 * DAYS 
      SANOM = SANOM * CONV
      SANOM = DMOD(SANOM,TWOPI) 
      IF (SANOM.LT.0D0) SANOM = SANOM + TWOPI 
C 
C  Ecliptic longitude and obliquity of the ecliptic 
C 
      ECLLON = SLON + 1.915D0 * DSIN(SANOM) + .02D0 * DSIN(2D0*SANOM) 
      ECLLON = ECLLON * CONV
      ECLLON = DMOD(ECLLON,TWOPI) 
      QUAD = ECLLON/(.5*PIE)
      IQUAD = 1 + QUAD
      OBLIQ = 23.439D0 - 4.D-7 * DAYS 
      OBLIQ = OBLIQ * CONV
C 
C  RA and DEC (RA is in the same quadrant as ecliptic longitude 
C 
      RA = DATAN(DCOS(OBLIQ) * DTAN(ECLLON))
      IF (IQUAD.EQ.2) RA = RA + PIE 
      IF (IQUAD.EQ.3) RA = RA + PIE 
      IF (IQUAD.EQ.4) RA = RA + TWOPI 
      DEC = DASIN(DSIN(OBLIQ) * DSIN(ECLLON)) 
C 
      RETURN
      END 
