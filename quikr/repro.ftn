FTN77,I,Y
$ALIAS /FSCOM/ , NOALLOCATE
$CDS ON
      SUBROUTINE REPRO(IP),SET UP REPRODUCE TRACKS C#870115:04:40#
C
C   REPRO controls the reproduce tracks in the tape controller
C
C     PROGRAMMER: NRV
C     LAST MODIFIED:  800829
C  WHO  WHEN    DESCRIPTION
C  GAG  910128  Added Reproduce Electronics logic to change tracks according
C               to RPRO_FS variable.
C
C     INPUT VARIABLES:
C
      DIMENSION IP(1)
C        IP(1)  - class number of input parameter buffer.
C
C     OUTPUT VARIABLES:
C
C        IP(1) - CLASS
C        IP(2) - # REC
C        IP(3) - ERROR
C        IP(4) - who we are
C
C     COMMON BLOCKS
C
      INCLUDE /FS/INCLUDE/FSCOM.FTNI
C
C     SUBROUTINE INTERFACE:
C
C     CALLED SUBROUTINES: GTPRM,JCHAR
C 
C     LOCAL VARIABLES 
C        NCHAR  - number of characters in buffer
C        IMMODE - mode for MAT
C        ICH    - character counter 
      DIMENSION IBUF(20)
C               - class buffer
C        ILEN   - length of IBUF, chars 
      DIMENSION IPARM(2)
C               - parameters returned from GTPRM
      DIMENSION IREG(2) 
C               - registers from EXEC calls 
C        ITA,ITB,IBW,IEQ,IBY
C               - variables for tracks, bandwidths, bypass
      DIMENSION BWS(7)
C               - lists for comparisons
C
      EQUIVALENCE (REG,IREG(1)),(PARM,IPARM(1))
C
C     INITIALIZED
      DATA ILEN/40/
      DATA BWS/0.0625,0.125,0.25,0.5,1.0,2.0,4.0/
C
C
C  1. If we have parameters, then we are to set the TP.
C     If no parameters, we have been requested to read the TP.
C
      ICHOLD = -99
      ICLCM = IP(1)
      IF (ICLCM.EQ.0) THEN
        IERR = -1
        GOTO 990
      ENDIF
      REG = EXEC(21,ICLCM,IBUF,-ILEN)
C     REG = EXEC(21,40000B+ICLCM,IBUF,-ILEN)
      NCHAR = MIN0(ILEN,IREG(2))
      IEQ = ISCNC(IBUF,1,NCHAR,75B)
C                   Scan for "="
      IF (IEQ.EQ.0) GOTO 500
C                   If no parameters, go read device
      IF (IEQ.EQ.NCHAR.OR.JCHAR(IBUF,IEQ+1).NE.77B) GOTO 140
      IP(1) = 0
      IP(4) = 77B
      CALL REPDS(IP,ICLCM)
      RETURN
C
140   IF (ICHCM(IBUF,IEQ+1,LTSRS,1,ILENTS).EQ.0) GOTO 600
      IF (ICHCM(IBUF,IEQ+1,LALRM,1,ILENAL).EQ.0) GOTO 700
C
C  2. Step through buffer getting each parameter and decoding it.
C     Command from user has these parameters:
C            REPRO=<bypass>,<trackA>,<trackB>,<bw>,<eq>
C     Choices are <trackA> and <trackB>: 1 to 28, defaults 1 and 2.
C                 <bw>, <eq>: 4,2,1,0.5,0.25,0.125,0.0625.  Default 2MHz
C                             for <bw>, default for <eq>=<bw>.
C                 <bypass>: BYPASS or ReadAfterWrite.  Default BYP.
C
C  2.1 BYPASS, PARAMETER 1
C
      ICH = 1+IEQ
      CALL GTPRM(IBUF,ICH,NCHAR,0,PARM)
      IF (JCHAR(IPARM,1).NE.54B.AND.JCHAR(IPARM,1).NE.52B) GOTO 211
      IF (JCHAR(IPARM,1).EQ.52B) IBY = IBYPAS
      IF (JCHAR(IPARM,1).EQ.54B) IBY = 1
C                   Default to bypass.
      GOTO 220
211   IBY = -1
      IF (ICHCM(PARM,1,4HBYP ,1,4).EQ.0) IBY = 1
      IF (ICHCM(PARM,1,4HRAW ,1,4).EQ.0) IBY = 0
      IF (IBY.NE.-1) GOTO 220
      IERR = -201
      GOTO 990
C
C  2.2 TRACK A, PARAMETER 2
C
220   CALL GTPRM(IBUF,ICH,NCHAR,1,PARM)
      IF (JCHAR(PARM,1).NE.52B.AND.JCHAR(PARM,1).NE.54B) GOTO 221
      IF (JCHAR(PARM,1).EQ.52B) ITA = ITRAKAUS_FS
      IF (JCHAR(PARM,1).EQ.54B) ITA = 1
C                   Default track is 1 for A
      GOTO 230
221   ITA = IPARM
      IF (ITA.GE.0.AND.ITA.LE.28) GOTO 230
      IERR = -202
      GOTO 990
C
C
C  2.3 TRACK B, PARAMETER 3
C
230   CALL GTPRM(IBUF,ICH,NCHAR,1,PARM)
      IF (JCHAR(PARM,1).NE.52B.AND.JCHAR(PARM,1).NE.54B) GOTO 231
      IF (JCHAR(PARM,1).EQ.52B) ITB = ITRAKBUS_FS
      IF (JCHAR(PARM,1).EQ.54B) ITB = 1
C                   Default for track B is 1
      GOTO 240
231   ITB = IPARM
      IF (ITB.GE.0.AND.ITB.LE.28) GOTO 240
      IERR = -203
      GOTO 990
C
C  CHECK FOR ODD AND EVEN TRACKS GIVEN FOR ODD OR EVEN ELECTRONICS
C
240   CONTINUE
      IF (
     .    ( (MOD(ITA,2).EQ.0.AND.MOD(ITB,2).NE.0).OR.
     .      (MOD(ITA,2).NE.0.AND.MOD(ITB,2).EQ.0)    ).AND.
     .    (ITA.NE.0.AND.ITB.NE.0)  .AND.IBY.NE.1.AND.
     .    (RPRO_FS.EQ.2.OR.RPRO_FS.EQ.1)
     .    ) THEN
        IERR = -206
        GOTO 990
      END IF
C
C  2.4 BANDWIDTH, PARAMETER 4
C
      CALL GTPRM(IBUF,ICH,NCHAR,2,PARM)
      IF (JCHAR(PARM,1).NE.54B.AND.JCHAR(PARM,1).NE.52B) GOTO 242
      IF (JCHAR(PARM,1).EQ.52B) IBW = IBWTAP
      IF (JCHAR(PARM,1).EQ.54B) IBW = 6
C                   Use 2 MHz bandwidth as default
      GOTO 250
242   DO 245 I=1,7
        IF (PARM.EQ.BWS(I)) GOTO 246
245     CONTINUE
      IERR = -204
      GOTO 990
246   IBW = I
C
C
C  2.5 EQUALIZER, PARAMETER 5
C
250   CALL GTPRM(IBUF,ICH,NCHAR,2,PARM)
      IF (JCHAR(IPARM,1).NE.54B.AND.JCHAR(IPARM,1).NE.52B) GOTO 251
      IF (JCHAR(IPARM,1).EQ.52B) IEQ = IEQTAP
      IF (JCHAR(IPARM,1).EQ.54B) IEQ = IBW - 1
C                   Default is for BW=EQ
      GOTO 300
251   DO 255 I=1,7
        IF (PARM.EQ.BWS(I)) GOTO 256
255     CONTINUE
      IERR = -205
      GOTO 990
256   IEQ = I-1
C
C
C
C  3. Now plant these values into COMMON.
C
300   ICHOLD = ICHECK(18)
      ICHECK(18) = 0
      ITRAKAUS_FS=ITA
      IF (ITA.NE.0.AND.IBY.NE.1) THEN
        IF (RPRO_FS.EQ.2) THEN         !EVEN
          IF (MOD(ITA,2).NE.0) ITA = ITA + 1
        ELSE IF (RPRO_FS.EQ.1) THEN    !ODD
          IF (MOD(ITA,2).EQ.0) ITA = ITA - 1
        ELSE IF (RPRO_FS.NE.0) THEN    !ALL
          IERR = -207
          GOTO 990
        END IF
      END IF
      ITRAKA = ITA
C
      ITRAKBUS_FS=ITB
      IF (ITB.NE.0.AND.IBY.NE.1) THEN
        IF (RPRO_FS.EQ.2) THEN         !EVEN
          IF (MOD(ITB,2).NE.0) ITB = ITB + 1
        ELSE IF (RPRO_FS.EQ.1) THEN    !ODD
          IF (MOD(ITB,2).EQ.0) ITB = ITB - 1
        ELSE IF (RPRO_FS.NE.0) THEN    !ALL
          IERR = -207
          GOTO 990
        END IF
      END IF
      ITRAKB = ITB
C
      IBWTAP = IBW
      IEQTAP = IEQ
      IBYPAS = IBY
C
C  4. Set up buffer for tape drive.  Send to MATCN.
C                   TP(rdebtbta
C
      IBUF(1) = 0
      IBUF(2) = 2HTP
      CALL RP2MA(IBUF(3),IBY,IEQ,IBW,ITA,ITB)
C
      ICLASS = 0
      CALL EXEC(20,0,IBUF,-13,2HFS,0,ICLASS)
C
      NREC = 1
      GOTO 800
C
C
C  5. This is the read device section.
C     Fill up three class buffers, one requesting ( data (mode -3),
C     one  ) (mode -4), one ! (mode -1).
C
500   IBUF(2) = 2HTP
      ICLASS = 0
      IBUF(1) = -1
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS)
C
      NREC = 1
      GOTO 800
C
C
C  6. This is the test/reset device section.
C
600   IBUF(1) = 6
      IBUF(2) = 2HTP
      ICLASS=0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS)
      NREC = 1
      GOTO 800
C
C
C  7. This is the alarm query and reset request.
C
700   IBUF(1) = 7
      IBUF(2) = 2HTP
      ICLASS=0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS)
      NREC = 1
      GOTO 800
C
C
C  8. All MATCN requests are scheduled here, and then RPDIS called.
C
800   CALL EXEC(23,6HMATCN ,ICLASS,NREC)
      CALL RMPAR(IP)
      IF(ICHOLD.NE.-99) ICHECK(18) = ICHOLD
      IF (ICHOLD.GE.0) THEN
         ICHECK(18) = MOD(ICHOLD,1000)+1
         KRPTP_FS=.TRUE.
      ENDIF
      CALL REPDS(IP,ICLCM)
      RETURN
C
C
990   IP(1) = 0
      IP(2) = 0
      IP(3) = IERR
      IP(4) = 2HQR
      RETURN
      END
