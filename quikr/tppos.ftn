FTN77,I,Y
$ALIAS /FSCOM/ , NOALLOCATE
$CDS ON
      SUBROUTINE TPPOS(IP),TAPE POSITION CONTROL C#870115:04:34#
C
C     TAPPOS sends commands to BOSS that control the position of the tape
C
C  WHO  WHEN    DESCRIPTION
C  GAG  910114  Changed LFEET to LFEET_FS and changed comparisons with
C               9600 to 20000.
C
C
C     INPUT VARIABLES:
      DIMENSION IP(1)
C        IP(1)  - class number of input parameter buffer.
C
C     OUTPUT VARIABLES:
C        IP(1) - CLASS
C        IP(2) - # REC
C        IP(3) - ERROR
C        IP(4) - who we are
C
C 2.2.   COMMON BLOCKS USED
      INCLUDE /FS/INCLUDE/FSCOM.FTNI
C
C 2.5.   SUBROUTINE INTERFACE:
C     CALLING SUBROUTINES: QUIKR
C     CALLED SUBROUTINES: GTPRM,JCHAR 
C 
C 3.  LOCAL VARIABLES 
C        NCHAR  - number of characters in buffer
C        ICH    - character counter 
      INTEGER*4 IDT4
      DIMENSION IBUF(20)                 !  class buffer
      DIMENSION IT(6)                    !  holds current time from system
      DIMENSION IPARM(2)                 !  parameters returned from GTPRM
      DIMENSION IREG(2)                  !  Registers from EXEC calls
      EQUIVALENCE (REG,IREG(1)),(PARM,IPARM(1)) 
C 
C 5.  INITIALIZED VARIABLES 
      DATA ILEN/40/                        !  Length of IBUF, characters
C 
C 
C     PROGRAM STRUCTURE 
C 
C     1. If there was "=" in the buffer, then we have parameters. 
C     If none, then error.  If parameter is "?" then give last command. 
C 
      IERR=0
      ICLCM = IP(1) 
      ICLASS = 0
      IF (ICLCM.EQ.0) THEN
        IERR = -1
        GOTO 990
      ENDIF
      REG = EXEC(21,ICLCM,IBUF,-ILEN)
      NCHAR = IREG(2) 
      IEQ = ISCNC(IBUF,1,NCHAR,75B)                  !  Scan for "="
      IF (IEQ.EQ.0) THEN
        IERR = -101
        GOTO 990
      ENDIF
      IF (JCHAR(IBUF,IEQ+1).EQ.77B) THEN
        NCH = ICHMV(IBUF,IEQ,2H/ ,1,1)
        NCH = NCH + IB2AS(IFTGO,IBUF,NCH,40000B+400B*4+4)
        ICLASS = 0
        CALL EXEC(20,0,IBUF,-NCH,2HFS,0,ICLASS)
        NREC = 1
        GOTO 990
      ENDIF
C 
C     2. Get the parameter (foot count) and decode it.
C               TAPEPOS=<position>
C
      ICH = 1+IEQ
      CALL GTPRM(IBUF,ICH,NCHAR,1,PARM)
C                   Get the position in feet
      IF (JCHAR(PARM,1).EQ.52B .OR. JCHAR(PARM,1).EQ.54B) THEN
        IERR = -101
        GOTO 990
      ENDIF
      IFT = IPARM
      IF (IFT.LT.0 .OR. IFT.GT.20000) THEN
        IERR = -201
        GOTO 990
      ENDIF
C
C     3. Get the tape motion and foot count status.
C     Then, make sure that the tape is stopped!  If not, we can't move it.
C     Check if the amount to move is within 100 feet.  If so, don't bother.
C
      IBUF(1) = -3
      IBUF(2) = 2HTP
      ICLASS = 0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS)
      CALL EXEC(23,6HMATCN ,ICLASS,1)
      CALL RMPAR(IP)
      IF (IP(3).LT.0) RETURN
      CALL EXEC(21,IP(1),IBUF,-ILEN)
      CALL MA2TP(IBUF,ILOW,LFEET_FS,IFASTP,ICAPTP,ISTPTP,ITACTP,IRDYTP)
C
D     WRITE(LU,9901) ICAPTP,IFT,LFEET_FS
D9901 FORMAT("ICAPTP,IFT,LFEET_FS="2I5,1X,2A2)
      ICLASS = 0
      NREC = 0
      IF (ICAPTP.NE.0) THEN
        IERR = -301
        GOTO 990
      ENDIF
      IFTNOW = IAS2B(LFEET_FS,1,5)
      IF (IFTNOW.GT.20000.OR.IFTNOW.LT.0) IFTNOW=0
C                   Consider out-of-range as 0
      IFTDIF = IABS(IFTNOW-IFT)
D     WRITE(LU,9902) IFTNOW,IFTDIF
D9902 FORMAT("IFTNOW,IFTDIF="2I5)
      IF (IFTDIF.LE.100) THEN
        IERR = -302
        GOTO 990
      ENDIF
C
C     4. Send the commands to BOSS.  Commands are:
C              RW@! or FF@! 
C          and ET@<now+feet/31fps>
C     Also, set commanded feet into common. 
C 
      IFTGO = IFT
      CALL EXEC(11,IT,IT(6))
D     WRITE(LU,9903) IT 
D9903 FORMAT("IT NOW="6I5)
      IDT4 = 0.5D0 + IACTTP + IFTDIF/.225D0
      IDT = MOD(IDT4,100J)
      CALL IADT(IT,IDT,1)
      IDT = IDT4/100J
      CALL IADT(IT,IDT,2) 
D     WRITE(LU,9904) IT,IDT 
D9904 FORMAT("IT INC="6I5", IDT="I5)
C                     Add on the time difference to the present 
      IBUF(1) = 2HFF
      IF (IFTNOW.GT.IFTGO) IBUF(1) = 2HRW 
      IBUF(2) = 2H@!
      IBUF(3) = 2HET
      IBUF(4) = 2H@ 
      IDUMM1 = IB2AS(IT(5),IBUF, 8,40000B+400B*3+3) 
      IDUMM1 = IB2AS(IT(4),IBUF,11,40000B+400B*2+2) 
      IDUMM1 = IB2AS(IT(3),IBUF,13,40000B+400B*2+2) 
      IDUMM1 = IB2AS(IT(2),IBUF,15,40000B+400B*2+2) 
      IDUMM1 = ICHMV(IBUF,17,2H..,1,1)
      IDUMM1 = IB2AS(IT(1),IBUF,18,40000B+400B*2+2)
      CALL COPIN(IBUF,4)
      CALL COPIN(IBUF(3),15)
C                     Send the commands to BOSS 
C 
990   IP(1) = ICLASS
      IP(2) = NREC
      IP(3) = IERR
      IP(4) = 2HQU
      RETURN
      END 
