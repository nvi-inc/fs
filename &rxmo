FTN4,X
C@RXMO
C 
      SUBROUTINE RXMO(IP),RECEIVER CONTROL C#780803:08:32#
C 
C     RXMO controls the S-X receiver and requests a reading 
C 
C     INPUT VARIABLES:
C 
      DIMENSION IP(1) 
C               - parameters from QUIKR 
C        IP(1)  - class number of input parameter buffer
C        IP(2-5)- not used
C 
C     OUTPUT VARIABLES: 
C 
C        IP(1) - CLASS
C        IP(2) - # RECS 
C        IP(3) - ERROR
C        IP(4) - who we are 
C 
C 2.2.   COMMON BLOCKS USED 
C 
      INCLUDE #FSCOM::FS
C 
C 2.5.   SUBROUTINE INTERFACE:
C 
C     CALLING SUBROUTINES:
C 
C     CALLED SUBROUTINES: RX2MA, MATCN, RXDMO 
C 
C 3.  LOCAL VARIABLES 
C 
C     LA - hex address
C     IC - code index for name checking 
C     DIMENSION LCODE(3,32) 
C      - code words for each address
C     DIMENSION NLCODE(32)
C      - number of characters in each code
C     NCODE - number of code words (must be the same as dimension 
C             in LCODE and NLCODE 
C     IDCAL - delay cal heater on/off 
C     IBOX - box heater off/A/B 
C     ICAL - noise cal on/off 
      DIMENSION IFAMP(3)
C           - IF amps on/off
C        NCHAR  - number of characters in uffer 
C        ICH    - character counter 
      DIMENSION IBUF(20)
C               - class buffer
C        ILEN   - length of IBUF, chars 
      DIMENSION IREG(2) 
C               - registers from EXEC calls 
C 
      EQUIVALENCE (REG,IREG(1)) 
C 
C 4.  CONSTANTS USED
C 
C 
C 5.  INITIALIZED VARIABLES 
C 
      DATA ILEN/40/ 
C 
C     DATA LCODE/ 
C    .2HFR,2HON,2HT ,2HRE,2HAR,2H  ,2HLO,2H  ,2H  ,2HDC,2HAL,2H  ,
C    .2HIN,2HLE,2HT ,2HSU,2HP ,2H  ,2HRE,2HT ,2H  , 
C    .2H-2,2H.7,2H3V,2HSI,2HF ,2H  ,2HXI,2HF ,2H  ,2HKI,2HF ,2H  ,
C    .2HSL,2HO ,2H  ,2HXL,2HO ,2H  ,2HKL,2HO ,2H  , 
C    .2HLO,2H5M,2HHZ,2HSP,2HAR,2HE ,2H28,2HV ,2H  ,2H24,2HV ,2H  ,
C    .2H20,2HV ,2H  ,2H15,2HVM,2H  ,2H5V,2H  ,2H  ,2H-1,2H5V,2H  ,
C    .2HGR,2HOU,2HND,2HPR,2HES,2H  ,
C    .2HX1,2HBI,2HAS,2HX2,2HBI,2HAS,2HX3,2HBI,2HAS,2HS1,2HBI,2HAS,
C    .2HS2,2HBI,2HAS,2HS3,2HBI,2HAS,2H20,2HK ,2H  ,2H70,2HK ,2H  /
C     DATA NLCODE/5,4,2,4,5,3,3,6,3,3,3,3,3,3,6,5,
C    .            3,3,3,3,2,4,6,4,6,6,6,6,6,6,3,3/
C     DATA NCODES/32/ 
C 
C 
C 6.  PROGRAMMER: NRV 
C     LAST MODIFIED:  CREATED 830610 AT MOJAVE
C          NRV 840509 MADE CHANGES FOR NEW VERSION
C          NRV 880615 MODIFIED PER LAR'S CHANGES WITH (RXDEF FILE 
C 
C 
C     1. If class buffer contains command name with "=" then we have
C     parameters to set the RX.  If only the command name is present, 
C     then read the RX. 
C 
      ICLCM = IP(1) 
      IERR = 0
      ICHOLD = -99
      IF (ICLCM.NE.0) GOTO 110
      IERR = -1 
      GOTO 990
110   REG = EXEC(21,ICLCM,IBUF,-ILEN) 
      NCHAR = MIN0(IREG(2),ILEN)
      IEQ = ISCNC(IBUF,1,NCHAR,75B) 
C                   Scan for "="
      IF (IEQ.EQ.0) GOTO 500
C                   If no parameters, go read RX
      IF (ICHAR(IBUF,IEQ+1).NE.77B) GOTO 140
      IP(4) = 77B 
      CALL RXDMO(IP,ICLCM)                
      RETURN
C 
140   CONTINUE
      IF (ICHCM(IBUF,IEQ+1,LTSRS,1,ILENTS).EQ.0) GOTO 600 
      IF (ICHCM(IBUF,IEQ+1,LALRM,1,ILENAL).EQ.0) GOTO 700 
C 
C 
C     2. Get parameter as follows:
C              RX=<channel>,<dcal>,<box>,<S>,<X>,<K>,<cal>
C     where <channel> may be a hex address or a code word.
C           <dcal> is ON or OFF for delay cal heater
C           <box> is A, B, or OFF for box heater
C           <S>,<X>,<K> are ON or OFF for IF amps 
C           <cal> is ON, OFF (normal) or OON, OOF (override)
C 
      ICH = 1+IEQ 
      IC1 = ICH 
      CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
C                     Get channel, ASCII
      NCH = ICH-IC1-1 
D     WRITE(LU,9903) ICH,NCH,IEQ
D9903 FORMAT("ICH,NCH,IEQ="3I5) 
      IF (NCH.GT.0) GOTO 210
C             There is no default for the channel 
      IERR = -101 
      GOTO 990
210   IF (ICHAR(IBUF,IC1).NE.52B) GOTO 211
      LA = IADCRX 
C             Pick up old address from common 
      GOTO 220
C 
211   IF (NCH.GT.2) GOTO 212
C     First check for an address
      LA = 2H00 
      IF (NCH.EQ.2) CALL ICHMV(LA,1,IBUF,IC1,2) 
      IF (NCH.EQ.1) CALL ICHMV(LA,2,IBUF,IC1,1) 
D      WRITE(LU,9901) NCH,IC1,LA  
D9901 FORMAT("NCH,ICH,LA="2I5,1X,A2)
      IF (IA22H(LA).NE.-1) GOTO 220 
C         This is a valid hex address 
C     Now check for a code word 
212   DO 213 IC=1,NCODES  
        IF (ICHCM(IBUF,IC1,LCODE(1,IC),1,NCH).EQ.0 .AND.      
     &    IFLCH(LCODE(1,IC),6).EQ.NCH) GOTO 214 
213     CONTINUE
      IERR = -201 
C             No match found for code word
      GOTO 990
214   IC = IC-1 
      LA = IH22A(IC)  
C 
C     2.2 Delay cal heater. ON or OFF.
C 
220   CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
      IF (ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B) GOTO 222 
      IF (ICHAR(PARM,1).EQ.52B) IDCAL=IDCHRX
C                     Pick up old value from common 
      IF (ICHAR(PARM,1).EQ.54B) IDCAL=1 
C                     Default is on 
      GOTO 230
222   IDCAL=-1
      IF (ICHCM(PARM,1,4HON  ,1,4).EQ.0) IDCAL=1
      IF (ICHCM(PARM,1,4HOFF ,1,4).EQ.0) IDCAL=0
      IF (IDCAL.GE.0) GOTO 230
      IERR = -202 
      GOTO 990
C 
C     2.3 Box heaters.  A, B, or OFF. 
C 
230   CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
      IF (ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B) GOTO 232 
      IF (ICHAR(PARM,1).EQ.52B) IBOX=IBXHRX 
C                     Pick up old value from common 
      IF (ICHAR(PARM,1).EQ.54B) IBOX=1  
C                     Default is A controller on
      GOTO 240
232   IBOX=-99
      IF (ICHCM(PARM,1,4HA   ,1,4).EQ.0) IBOX=1 
      IF (ICHCM(PARM,1,4HB   ,1,4).EQ.0) IBOX=-1
      IF (ICHCM(PARM,1,4HOFF ,1,4).EQ.0) IBOX=0 
      IF (IBOX.NE.-99) GOTO 240 
      IERR = -203 
      GOTO 990
C 
C     2.4 IF amplifiers, S-X-K are ON or OFF. 
C 
240   DO 249 I=1,3
        CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
        IF (ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B) GOTO 242 
        IF (ICHAR(PARM,1).EQ.52B) IFAMP(I)=IFAMRX(I)
C                     Pick up old value from common 
        IF (ICHAR(PARM,1).EQ.54B) IFAMP(I)=1
C                     Default is on 
        GOTO 249
242     IFAMP(I)=-1 
        IF (ICHCM(PARM,1,4HON  ,1,4).EQ.0) IFAMP(I)=1 
        IF (ICHCM(PARM,1,4HOFF ,1,4).EQ.0) IFAMP(I)=0 
        IF (IFAMP(I).NE.-1) GOTO 249
        IERR = -203-I 
        GOTO 990
249     CONTINUE
C 
C     2.5 Noise cal, ON or OFF or EXT, or OON or OOFF for override. 
C 
250   CALL GTPRM(IBUF,ICH,NCHAR,0,PARM) 
      IF (ICHAR(PARM,1).NE.52B.AND.ICHAR(PARM,1).NE.54B) GOTO 252 
      IF (ICHAR(PARM,1).EQ.52B) ICAL=LSWCAL 
C                     Pick up old value from common 
      IF (ICHAR(PARM,1).EQ.54B) ICAL=0  
C                     Default is off
      GOTO 300
252   ICAL=-99
      IF (ICHCM(PARM,1,4HON  ,1,4).EQ.0) ICAL=1 
      IF (ICHCM(PARM,1,4HOFF ,1,4).EQ.0) ICAL=0 
      IF (ICHCM(PARM,1,4HOON ,1,4).EQ.0) ICAL=-1
      IF (ICHCM(PARM,1,4HOOFF,1,4).EQ.0) ICAL=-2
      IF (ICHCM(PARM,1,4HEXT ,1,4).EQ.0) ICAL=2 
      IF (ICAL.NE.-99) GOTO 300 
      IERR = -207 
      GOTO 990
C 
C 
C     3. Finally, format the buffer for the controller. 
C 
300   IBUF(1) = 0 
      IBUF(2) = 2HRX
      CALL RX2MA(IBUF(3),ICAL,0,IDCAL,IBOX,IFAMP,LA)
C                   Format the data part of the buffer
      ICLASS = 0
      CALL EXEC(20,0,IBUF,-12,2HFS,0,ICLASS)
      NREC = 1
      IF (IDCAL.EQ.IDCHRX.AND.IBOX.EQ.IBXHRX) GOTO 400
      CALL RX2MA(IBUF(3),ICAL,1,IDCAL,IBOX,IFAMP,LA)
C                      Send second message to effect transition 
      CALL EXEC(20,0,IBUF,-12,2HFS,0,ICLASS)
      NREC = 2
      GOTO 400
C 
C 
C     4. Now plant these values into COMMON.
C     Finally schedule BOSS to request that MATCON gets the data. 
C 
400   CONTINUE
      ICHOLD = ICHK19   
      ICHK19 = 0
      IADCRX = LA 
      LSWCAL = ICAL 
      IDCHRX = IDCAL
      IBXHRX = IBOX 
      IFAMRX(1) = IFAMP(1)
      IFAMRX(2) = IFAMP(2)
      IFAMRX(3) = IFAMP(3)
C 
      GOTO 800
C 
C 
C     5.  This is the read device section.
C     Request one type of data. 
C 
500   IBUF(1) = -1
      IBUF(2) = 2HRX
      ICLASS = 0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS) 
      NREC = 1
      GOTO 800
C 
C 
C     6. This is the test/reset device section. 
C 
600   IBUF(1) = 6 
      IBUF(2) = 2HRX
      ICLASS=0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS) 
      NREC = 1
      GOTO 800
C 
C 
C     7. This is the alarm query and reset request. 
C 
700   IBUF(1) = 7 
      IBUF(2) = 2HRX
      ICLASS=0
      CALL EXEC(20,0,IBUF,-4,2HFS,0,ICLASS) 
      NREC = 1
      GOTO 800
C 
C 
C     8. All MATCN requests are scheduled here, and then RXDMO called.
C 
800   CALL EXEC(23,6HMATCN ,ICLASS,NREC)
      CALL RMPAR(IP)
      IF (ICHOLD.NE.-99) ICHK19 = ICHOLD
      IF (ICHOLD.GE.0) ICHK19 = 1 
      CALL RXDMO(IP,ICLCM)                
      RETURN
C 
990   IP(1) = 0 
      IP(2) = 0 
      IP(3) = IERR
      IP(4) = 2HQB
      RETURN
      END 
