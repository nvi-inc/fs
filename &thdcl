FTN4,X
C@THDCL 
C 
      SUBROUTINE THDCL(IP,IPS,IHEAD,FSTFW,SLWFW,FSTRV,  
     +                SLWRV),CALIBRATE HEAD POSITIONER SPEEDS   
C 
C 
      INCLUDE #FSCOM::FS
C 
C Starting in centered position, THDCL moves the head block in both 
C  directions at both fast and slow speed, to calibrate the speeds. 
C 
C  INPUT VARIABLES: 
      DIMENSION IP(1),PNOW(2),FSTFW(2),SLWFW(2),FSTRV(2),SLWRV(2) 
C 
C  LOCAL VARIABLES: 
C 
C  OUTPUT VARIABLES:
C     IP - RMPAR variables from MATCN 
C    POS - UPDATED HEAD POSITION(S) 
C FSTFW - UPDATED FAST FORWARD SPEED(S) 
C SLWFW - UPDATED SLOW FORWARD SPEED(S) 
C FSTRV - UPDATED FAST REVERSE SPEED(S) 
C SLWRV - UPDATED SLOW REVERSE SPEED(S) 
C 
C  HISTORY: 
C 
C  DATE  WHO  WHAT
C 850104 MWH  CREATED 
C 880615 NRV  MODIFIED PER LAR'S CHANGES FOR NEW TAPE FORMAT
C 
C 
C  1. First calibrate fast forward speed. 
C 
      DO 490 I=1,2
        IF (IHEAD.NE.3.AND.IHEAD.NE.I) GOTO 490 
        PSAVE = PNOW(I) 
        CALL HMOVE(I,1,1,2500,IP) 
C         - move fast forward for 0.1 second
        IF (IP(3).LT.0) RETURN
       CALL SUSP(1,15)
        CALL POSIT(I,IPS,PNOW(I),IP)
        IF (IP(3).LT.0) RETURN
        FSTFW(I) = ABS((PNOW(I)-PSAVE)/0.1) 
C 
C  2. Next calibrate fast reverse speed.
C 
      PSAVE = PNOW(I) 
      CALL HMOVE(I,1,0,2500,IP) 
C        - move fast reverse for 0.1 second 
      IF (IP(3).LT.0) RETURN
      CALL SUSP(1,15) 
      CALL POSIT(I,IPS,PNOW(I),IP)
      IF (IP(3).LT.0) RETURN
      FSTRV(I) = ABS((PNOW(I)-PSAVE)/0.1) 
C 
C  3. Now check out slow forward. 
C 
      PSAVE = PNOW(I) 
      CALL HMOVE(I,0,1,25000,IP)
      IF (IP(3).LT.0) RETURN
      CALL SUSP(2,1)
      CALL SUSP(1,5)
      CALL POSIT(I,IPS,PNOW(I),IP)
      IF (IP(3).LT.0) RETURN
      SLWFW(I) = ABS((PNOW(I)-PSAVE)/1.0) 
C 
C  4. Finally check slow reverse. 
C 
      PSAVE = PNOW(I) 
      CALL HMOVE(I,0,0,25000,IP)
      IF (IP(3).LT.0) RETURN
      CALL SUSP(2,1)
      CALL SUSP(1,5)
      CALL POSIT(I,IPS,PNOW(I),IP)
      IF (IP(3).LT.0) RETURN
      SLWRV(I) = ABS((PNOW(I)-PSAVE)/1.0) 
D     WRITE(LU,9001)I,FSTFW(I),FSTRV(I),SLWFW(I),SLWRV(I) 
D9001 FORMAT("FF,FR,SF,SR FOR HEAD "I3" = "4F10.4)
490   CONTINUE
      RETURN
      END 
