FTN77,I,Y
$CDS ON
      subroutine bsort(istart,length,ncode,bufr)          !  <910323.0052>
c  Sort bar codes by length and ASCII order        Lloyd Rawley    March 1988
c
c  Calls COPIN and character subroutines, called by RWAND.
c
c  Input parameters:
      integer istart(ncode),length(ncode),ncode,bufr(*)
c   bufr:    ASCII array containing all of the bar codes
c   ncode:   Number of bar codes            
c   istart:  Array of indexes within bufr of the beginning of a bar code
c   length:  Array of lengths of the bar codes
c
c  On output, ncode and bufr are unchanged.  The other arrays are reorganized
c   so that the shortest strings come first, and each group of strings of the
c   same length is alphabetized by ASCII collating sequence.  In addition, if
c   more than one tape label (distinguished from other bar codes by its
c   length) is present in the list, all except the last one are considered
c   errors and are replaced with the last value.  This value is sent to the
c   LABEL command for logging.
      parameter (lentap=10)          !  8 characters plus space plus checksum
c
      logical changes         !  Used to determine when the sort is complete
      integer lmess(8)        !  Ersatz message from operator to BOSS
c
c  1. Make all tape label references refer to the last label.
c
      lasttap = 0
      do i=ncode,1,-1
        if (length(i).eq.lentap) then
          if (lasttap.eq.0) then
            lasttap = i
          else
            istart(i) = istart(lasttap)
          endif
        endif
      enddo
c
c  2. Send the tape label (if any) to the QUIKR LABEL command via COPIN.
c
      if (lasttap.ne.0) then
        call ichmv(lmess,1,6hLABEL=,1,6)
        call ichmv(lmess,7,bufr,istart(lasttap),lentap)
        call mcoma(lmess,17)
        call copin(lmess,7+lentap)
      endif
c
c  3. Bubble sort
c
      changes = .true.
      do while(changes)
        changes = .false.
        do i=2,ncode
          lendif = length(i)-length(i-1)
          if (lendif.lt.0) then        !  If shorter string comes later, swap
            itmp = length(i)
            length(i) = length(i-1)
            length(i-1) = itmp
            itmp = istart(i)
            istart(i) = istart(i-1)
            istart(i-1) = itmp
            changes = .true.
c    Don't sort 8-character codes so that VC's can be displayed in order
          else if (lendif.eq.0 .and. length(i).ne.8 .and. !  ASCII comparison
     &    ichcm(bufr,istart(i),bufr,istart(i-1),length(i)).gt.0) then
            itmp = istart(i)                      !  Swap
            istart(i) = istart(i-1)
            istart(i-1) = itmp
            changes = .true.
          endif
        enddo
      enddo
      end
