ASMB
      NAM SQISH,8  SQUEEZE OUT PARITY BITS  ARW 781004
      ENT SQISH 
      EXT .ENTR,.GOTO 
* 
*     CALL SUBROUTINE SQISH(IW,IWBIT,JW,JWBYT,NBYTES,IPERR,TABLE) 
* 
*STARTING WITH BIT IWBIT OF WORD IW, SQUEEZES OUT PARITY BITS FROM
*NBYTES 9-BIT CHARS AND PLACES THE RESULTS (NBYTES 8-BIT CHARS) INTO
*JW STARTING AT BYTE JWBYT. MAX VALUE OF NBYTES IS 3640-IWBIT 9.
*INCREMENTS IPERR FOR EVERY EVEN PARITY BYTE FOUND. 
*ON RETURN, IWBIT=IWBIT+9*NBYTES AND JWBYT=JWBYT+NBYTES.
*FIRST BIT IN IW IS BIT #1. 
*FIRST BYTE IN JW IS BYTE #1. 
* 
*NOTE!!!--IWBIT,JWBYT,IPERR MAY BE MODIFIED ON RETURN!!!! 
* 
A     EQU 0 
B     EQU 1 
* 
IW    NOP 
IWBIT NOP 
JW    NOP 
JWBYT NOP 
NBYTE NOP 
PERRS NOP 
TABLE NOP 
SQISH NOP 
      JSB .ENTR 
      DEF IW
      LDB JW        SET UP CHARACTER ADDRESS
      RBL 
      ADB JWBYT,I 
      ADB =B-1
      STB JW
      LDA NBYTE,I   SET UP BYTE COUNTER 
      CMA,INA 
      SSA,RSS       NBYTE<=0? 
      JMP SQISH,I    YES. RETURN
      STA COUNT 
      CLA 
      LDB IWBIT,I 
      ADB =B-1
      LSR 4         DIVIDE BY 16 TO GET STARTING WORD IN IW 
      ADB IW
      STB IW
      ALF           GET REMAINDER--START BIT IN IW (0-15) 
      SZA,RSS 
      JMP S0        START ON WORD BOUNDARY
      STA TEMP
      IOR LSL       CREATE LSL INSTRUCTION
      STA LSLX
      LDA IW,I      INITIALIZE A-REG
LSLX  LSL 16
      JSB .GOTO 
      DEF S0
      DEF TEMP
      DEF S1
      DEF S2
      DEF S3
      DEF S4
      DEF S5
      DEF S6
      DEF S7
      DEF S8
      DEF S9
      DEF S10 
      DEF S11 
      DEF S12 
      DEF S13 
      DEF S14 
      DEF S15 
* 
S0    LDA IW,I
      LSL 8 
      ELA 
      JSB PARTY 
S9    LSL 7 
      ISZ IW
      LDA IW,I
      LSL 1 
      ELA 
      JSB PARTY 
S2    LSL 8 
      ELA 
      JSB PARTY 
S11   LSL 5 
      ISZ IW
      LDA IW,I
      LSL 3 
      ELA 
      JSB PARTY 
S4    LSL 8 
      ELA 
      JSB PARTY 
S13   LSL 3 
      ISZ IW
      LDA IW,I
      LSL 5 
      ELA 
      JSB PARTY 
S6    LSL 8 
      ELA 
      JSB PARTY 
S15   LSL 1 
      ISZ IW
      LDA IW,I
      LSL 7 
      ELA 
      JSB PARTY 
S8    LSL 8 
      ISZ IW
      LDA IW,I
      ELA 
      JSB PARTY 
S1    LSL 8 
      ELA 
      JSB PARTY 
S10   LSL 6 
      ISZ IW
      LDA IW,I
      LSL 2 
      ELA 
      JSB PARTY 
S3    LSL 8 
      ELA 
      JSB PARTY 
S12   LSL 4 
      ISZ IW
      LDA IW,I
      LSL 4 
      ELA 
      JSB PARTY 
S5    LSL 8 
      ELA 
      JSB PARTY 
S14   LSL 2 
      ISZ IW
      LDA IW,I
      LSL 6 
      ELA 
      JSB PARTY 
S7    LSL 8 
      ELA 
      JSB PARTY 
      ISZ IW
      JMP S0
* 
*PARITY CHECK AND BYTE STORE
PARTY NOP 
      CAX 
      LDA B 
      LDB JW
      SBT           STORE BYTE
      STB JW        STORE INCREMENTED ADDRESS 
      AND =B377 
      ADA TABLE 
      LDA A,I       GET # OF 1's IN 8-BIT CHAR
      SEZ           PARITY BIT=1? 
      INA            YES. SO INCREMENT A-REG. 
      SLA,RSS       TOTAL # OF 1's EVEN?
      ISZ PERRS,I    YES. SO INCREMENT PARITY ERROR COUNT.
      CXA 
      ISZ COUNT     INCREMENT BYTE COUNTER
      JMP PARTY,I 
      LDA JWBYT,I   UPDATE JWBYT
      ADA NBYTE,I 
      STA JWBYT,I 
      LDA NBYTE,I   UPDATE IWBIT
      MPY =D9 
      ADA IWBIT,I 
      STA IWBIT,I 
      JMP SQISH,I   RETURN
* 
COUNT BSS 1 
TEMP  BSS 1 
LSL   LSL 16
* 
      END 
