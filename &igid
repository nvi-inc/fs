ASMB,L,T,C
        HED     GET THE ADDRESS OF ID SEGMENT NAMED 
        NAM     IGID,7 C#890516:19:11    GET NAMED IDSEG ADDR V=2#
        ENT     IGID
        EXT     .ENTR,$LIBR,$LIBX 
* 
*               THIS ROUTINE RETURNS THE ADDRESS OF THE ID SEGMENT NAMED
*       IN NAME IN THE A REG AND THE OFFSET OF THIS IN THE KEYWD TABLE IN 
*       THE B REG.  IF NAME IS ZERO IN THE FIRST WORD THE THE ID SEGMENT OF 
*       THE CURRENT PROGRAM IS RETURNED AS IS ITS OFFSET. 
* 
* 
NAME    BSS     1               THE ADDRESS OF THE NAME FIELD OR ZERO 
IGID    NOP                     THE ENTRY POINT 
        JSB     .ENTR           GET THE BOOKKEEPING DONE
        DEF     NAME            TELL TOP OF PARAMETER LIST
        JSB     $LIBR           TURN OFF THE FENCE FOR UNLIMITED ACCESS 
        DEF     0               TELL FENCE OF TYPE OF CALL
        LDA     NAME,I          GET THE FIRST WORD OF THE NAME FIELD
        CPA     =D0             CHECK FOR OUR ID ADDRESS SOUGHT 
        JMP     *+2             SKIP OVER IF OUR ID WANTED
        JMP     SCNID           SEARCH NEEDED GO TO HARD SECTION
        LDA     1717B           GET OUR ID NOW
        LDB     1657B           GET FIRST IDSEG ADDRESS 
        CMB,INB                 MAKE IT NEGATIVE
        ADA     1               GET DIFFERENCE BETWEEN ID ADDRESSES 
        CLB                     SET UP FOR DIVIDE 
        DIV     =D29
        LDB     0               MOVE OFFSET TO B REG
        INB                     START COUNTING FROM 1 NOT 0 
        LDA     1717B           GET OUR ADRESS
        JMP     GEXIT           GO EXIT AND TURN ON MEMORY FENCE
SCNID   LDB     1657B           GET POINTER TO FIRST IDSEG ADDRESS
        STB     IDPTR           SAVE FOR LOOP WORK
        LDB     IDPTR,I         GET FIRST IDSEG ADDRESS 
NXTID   ADB     =D12            POINT AT ITS NAME FIELD 
        LDA     NAME            GET NAME POINTER FOR CHARACTER COMPARE
        ASL     1               MAKE AREG AND BREG BYTE ADDRESSES 
        CBT     =D5             COMPARE 5 CHARCTERS 
        JMP     GETOF           JUMP TO END FOR MATCH 
        NOP                     SKIP LESS THAN CASE 
        ISZ     IDPTR           STEP TO LOOK AT NEXT IDSEG
        LDB     IDPTR,I         CHECK FOR END OF LIST NOW 
        CPB     =D0             IF ZERO NOW NO FIND THIS REQUEST
        JMP     NOFND           SKIP OUT IF NO FIND 
        JMP     NXTID           LOOP BACK AND TRY THE NEXT ID SEG 
NOFND   CLA                     RETURN ZERO ADDRESS FOR NO FIND 
        CLB                     RETURN ZERO OFFSET FOR NO FIND ALSO 
        JMP     GEXIT           RETURN TO CALLER ALL IS DONE
GETOF   LDA     IDPTR,I         GET THE ID SEG ADDRESS
        LDB     1657B           GET FIRST ID ADDRESS
        CMB,INB                 GET MINUS VALUE 
        ADB     IDPTR           GET OFFSET FROM FIRST 
        INB                     START COUNT FROM 1 INSTEAD OF ZERO
GEXIT   STA     IDPTR           SAVE IDSEG ADDRESS
        STB     IDOFF           SAVE OFFSET VALUE FOR RETURN
        JSB     $LIBX           LEAVE UNPROCTECTED MODE 
        DEF     *+1 
        DEF     *+1 
        LDA     IDPTR           GET IDSEG ADDRESS FOR RETURN
        LDB     IDOFF           GET OFF SET FOR RETURN
        JMP     IGID,I          GO BACK TO USER 
* 
*               INTERNALLY USED LOCATIONS 
* 
IDPTR   DEF     0               POINTER TO IDSEG ADDRESSES
IDOFF   DEF     0               OFFSET VALUE FOR IDSEG
* 
        END 
        END$
