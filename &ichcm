ASMB,R,B,L
        HED     CHARACTER MOVE ROUTINE
* 
*@ICHCM 
* 
*       INTEGER FUNCTION ICHCM(IOUT,IOI,IN,INI,N) 
* 
*     ICHCM 
* 
* 1.  NAME PROGRAM SPECIFICATION
* 
* 1.1.   ICHCM
* 
*		THIS ROUTINE COMPARES N CHARACTERS IN IN TO N CHARACTERS 
*	IN IOUT.  IF THEY ARE EQUAL A ZERO IS RETURNED.  IF THEY ARE
*	DIFFERENT THE INDEX OF THE FIRST NON-EQUAL CHARACTER IS RETURNED
*	AND THE SIGN INDICATES WHETHER THE SECOND FIELD(IN) IS GREATER(+) 
*	OR LESS(-) THAN THE FIRST FIELD(IOUT) 
* 
* 1.2.   RESTRICTIONS - limits on use of routine
* 
* 1.3.   REFERENCES - document cited
* 
* 2.  NAME INTERFACE
* 
* 2.1.   CALLING SEQUENCE: IEND=ICHCM(IOUT,IOI,IN,INI,N)
* 
*     INPUT VARIABLES:
* 
*       IOI     - INITIAL CHARACTER POSITION TO COMPARE IN IOUT 
* 
*       INI     - INITIAL CHARACTER TO COMPARE IN IN
* 
*       N       - NUMBER OF CHARACTERS TO COMPARE 
* 
*       DIMENSION IN(2) 
* 
*       IN      - THE SECOND CHARACTERS TO BE COMPARED TO IOUT
* 
*     OUTPUT VARIABLES: 
* 
*	ICHCM	- THE NUMBER AND ARITHIMETIC DIFFERENCE OF THE FIRST
*		  NON-EQUAL CHARACTERS 
* 
*		= 0	- STRINGS EQUAL
*		= +N	- N-TH CHARACTER IS DIFFERENT AND IN+INI+N GREATER
*		= -N	- N-TH CHARACTER IS DIFFERENT AND IN+INI+N SMALLER
* 
*       DIMENSION IOUT(2) 
* 
*       IOUT    - THE FIRST STRING IN THE COMPARISON
* 
* 2.5.   SUBROUTINE INTERFACE:
* 
*       THIS IS A UTILITY TO BE CALLED BY ANYONE
* 
*     CALLED SUBROUTINES: NONE
* 
* 3.  LOCAL VARIABLES 
* 
* 
* 6.  PROGRAMMER: LEE N. FOSTER 
*     LAST MODIFIED:
*C# (Filled by COMPC with the current date) # 
* 
        NAM     ICHCM,7 CHARACTER COMPARING ROUTINE C#890516:19:09    # 
        ENT     ICHCM 
        EXT     .ENTR 
IOUT    BSS     1 
IOI     BSS     1 
IN      BSS     1 
INI     BSS     1 
N       BSS     1 
ICHCM   NOP             MAIN ENTRY POINT
        JSB     .ENTR   MOVE PARAMETERS INTO ROUTINE
        DEF     IOUT
        LDA     IOUT    GET START WORD ADDRESS
        RAL             MAKE A BYTE ADDRESS 
        ADA     DMONE   ADJUST FOR ZERO BYTE PROBLEM
        STA     ASTRT   SAVE START IN A 
        ADA     IOI,I   GET INPUT BYTE ADDRESS
        LDB     IN      GET OUTPUT WORD ADDRESS 
        RBL             MAKE A WORD ADDRESS 
        ADB     DMONE   HANDLE ZERO BYTE ADDRESSING PROBLEM 
        ADB     INI,I   GET OUTPUT BYTE ADDRESS 
        CBT     N,I     COMPARE N BYTES IN IOUT TO IN 
        JMP     EQUAL 
        JMP     LESS
        CMA,INA         MAKE IT NEGATIVE
        ADA     ASTRT   GET THE OFFSET ONLY 
        JMP     ICHCM,I RETURN TO CALLER
EQUAL   CLA             SET FOR A ZERO RETURN 
        JMP     ICHCM,I RETRUN TO CALLER
LESS    LDB     ASTRT   GET INITIAL A REGISTER
        CMB,INB         MAKE IT NEGATIVE
        ADA     1       ADD IT TO CURRENT A REG 
        JMP     ICHCM,I RETRUN TO CALLER
ASTRT   DEF     0 
DMONE   DEC     -1
        END 
