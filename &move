FTN4,X
      SUBROUTINE MOVE (NYRI,NYRF,MO,NDA,RA,D,  DELR,DELD,DC)
C 
C  WTW'S HP VERSION OF BALL'S MOVE ROUTINE 2/8/77 
C 
C 
C  MOVE CALCULATES THE CORRECTION (DELR) IN RIGHT ASCENSION (RA) AND THE
C  CORRECTION (DELD) IN DECLINATION (D) (ALL IN RADIANS) TO BE ADDED TO THE 
C  MEAN COORDINATES FOR EPOCH NYRI (E.G. 1950) TO GIVE THE APPARENT POSITIONS 
C  OF A DATE SPECIFIED BY THE YEAR (NYRF, E.G. 1968), MONTH (MO, 1 TO 12), AND
C  DAY (NDA).  IF THE DAY-NUMBER IS KNOWN, USE IT FOR NDA AND SET MO = 1. 
C  MOVE ALSO CALCULATES THE EQUATION OF THE EQUINOXES (DC, IN MINUTES OF TIME)
C  WHICH MAY BE ADDED TO THE MEAN SIDEREAL TIME TO GIVE THE APPARENT SIDEREAL 
C  TIME (AENA-469).  DELR AND DELD CONTAIN CORRECTIONS FOR PRECESSION, ANNUAL 
C  ABERRATION, AND SOME TERMS OF NUTATION.  IF RA AND D ARE FOR THE MEAN EPOCH
C  (I.E. HALFWAY BETWEEN NYRI AND NYRF) THEN THE PRECISION OF DELR AND DELD IS
C  ABOUT 2 ARCSECONDS (SEE NEGLECTED TERMS IN ESE-44).  IF RA AND D ARE EITHER
C  OF THE END POINTS OF THE INTERVAL, THEN THE PRECISION MAY BE SOMEWHAT WORSE. 
C  AENA = THE AMERICAN EPHEMERIS AND NAUTICAL ALMANAC (THE BLUE BOOK).
C  ESE = THE EXPLANATORY SUPPLEMENT TO ABOVE (THE GREEN BOOK).
C 
      DOUBLE PRECISION RA,D,DELR,DELD,DC,SND,CSD,TND,CSR,SNR, 
     1 AL,TO,T,ZETAO,Z,THETA,AM,AN,ALAM,SNL,CSL,OMEGA,ARG,
     2 DLONG,DOBLQ,COSF,SINF,FLOATF 
      COSF(V)=DCOS(V) 
      SINF(V)=DSIN(V) 
      FLOATF(II)=DBLE(FLOAT(II))
      SND=SINF(D) 
      CSD=COSF(D) 
      TND=SND/CSD 
C 
      CSR=COSF(RA)
      SNR=SINF(RA)
C 
C  AL IS AN APPROXIMATE DAY NUMBER (I.E. THE NUMBER OF DAYS SINCE JANUARY 0 
C  OF THE YEAR NYRF). 
      AL=30*(MO-1)+NDA
C 
C  TO IS THE TIME FROM 1900 TO NYRI (CENTURIES) 
      TO=FLOATF(NYRI-1900)/100.0
C  T IS THE TIME FROM NYRI TO DATE (NYRF, MO, NDA) (CENTURIES)
C  (365.2421988 IS THE NUMBER OF EPHEMERIS DAYS IN A TROPICAL YEAR) 
      T=(FLOATF(NYRF-NYRI)+AL/365.2421988D0)/100.0
C  ZETAO IS A PRECESSIONAL ANGLE FROM ESE-29 (ARCSECONDS) 
      ZETAO=(2304.250D0+1.396*TO)*T+0.302*T**2+0.018*T**3 
C  DITTO FOR Z
      Z=ZETAO+0.791*T**2
C  AND THETA
      THETA=(2004.682D0-0.853*TO)*T-0.426*T**2-0.042*T**3 
C  AM AND AN ARE THE M AND N PRECESSIONAL NUMBERS (SEE AENA-50, 474) (RADIANS)
      AM=(ZETAO+Z)*4.848136811D-6 
      AN=THETA*4.848136811D-6 
C 
C  ALAM IS AN APPROXIMATE MEAN LONGITUDE FOR THE SUN (AENA-50) (RADIANS)
      ALAM=(0.985647D0*AL+278.5)*0.0174532925D0 
      SNL=SINF(ALAM)
      CSL=COSF(ALAM)
C  DELR IS THE ANNUAL ABERRATION TERM IN RA (RADIANS) (ESE-47,48) 
C  (0.91745051 = COS(OBLIQUITY OF ECLIPTIC))
C  (-9.92413605E-5 = K = 20.47 ARCSECONDS = CONSTANT OF ABERRATION (ESE-48))
      DELR=-9.92413605D-5*(SNL*SNR+0.91745051D0*CSL*CSR)/CSD
     2 +AM+AN*SNR*TND 
C  PLUS PRECESSION TERMS (SEE AENA-50 AND ESE-38) 
C  DELD IS DITTO ABOVE IN DECLINATION 
      DELD=-9.92413605D-5*(SNL*CSR*SND-0.91745051D0*CSL*SNR*SND 
     2 +0.39784993D0*CSL*CSD) +AN*CSR 
C  (0.39784993 = SIN(OBLIQUITY OF ECLIPTIC))
C 
C  THE FOLLOWING CALCULATES THE NUTATION (APPROXIMATELY) (ESE-41,45)
C  OMEGA IS THE ANGLE OF THE FIRST TERM OF NUTATION (ESE-44) (APPROXIMATE 
C  FORMULA) (DEGREES) 
      OMEGA=259.183275D0-1934.142D0*(TO+T)
C  ARG IS OMEGA CONVERTED TO RADIANS
      ARG=OMEGA*0.0174532925D0
C  DLONG IS THE NUTATION IN LONGITUDE (DELTA-PSI) (RADIANS) 
      DLONG=-8.3597D-5*SINF(ARG)
C  DOBLQ IS THE NUTATION IN OBLIQUITY (DELTA-EPSILON) (RADIANS) 
      DOBLQ= 4.4678D-5*COSF(ARG)
C 
C  ADD NUTATION IN RA INTO DELR (ESE-43)
      DELR=DELR+DLONG*(0.91745051D0+0.39784993D0*SNR*TND)-CSR*TND*DOBLQ 
C  AND DEC. 
      DELD=DELD+0.39784993D0 *CSR*DLONG+SNR*DOBLQ 
C  DC IS THE EQUATION OF THE EQUINOXES (MINUTES OF TIME) (ESE-43) 
      DC=DLONG*210.264169D0 
      RETURN
      END 
      END$
